
#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Обработчики операций

Функция Ping()
	
	ПроверитьБлокировкуИнформационнойБазыДляОбновления();
	
	Возврат Истина;
	
КонецФункции

Функция Version()
	
	Возврат "1.0.1.1";
	
КонецФункции

// Передача активных пользователей по ИД_Системы
//
// Параметры:
//  ID_System	 - Строка - идентификация по реквизиту ИД_ИнформационнойБазы справочника "СписокИнформационныхБаз1С"
// 
// Возвращаемое значение:
//  ОбъектXDTO - см. ОбъектXDTO.AccessControlData.ResponseActiveUsers 
//
Функция GetActiveUsers(ID_System)
	
	// логирование
	ИмяОперации = "Администрирование.УправлениеСетью.УправлениеДоступомПользователей.GetActiveUsers";
	ТекстИнформации = СтрШаблон(НСтр("ru = 'Начало работы с системой %1'"), ID_System); 
	ОбщегоНазначенияКупоросСервер.ЗаписьЖурналаРегистрацииРасширенная(ИмяОперации, ТекстИнформации, УровеньЖурналаРегистрации.Информация);
	
	ПроверитьБлокировкуИнформационнойБазыДляОбновления();
	
	// - Получение таблицы активных пользователей из группы AD.
	ТаблицаПользователей = УправлениеДаннымиВнешнихСистем.ТаблицаАктивныхПользователейПоГруппеAD(ID_System);
	
	// - Формирование ответа.
	ОтветXDTO = УправлениеДаннымиВнешнихСистем.ОтветResponseActiveUsers(ТаблицаПользователей);

	// логирование
	ТекстИнформации = СтрШаблон(НСтр("ru = 'Конец работы с системой %1'"), ID_System); 
	ОбщегоНазначенияКупоросСервер.ЗаписьЖурналаРегистрацииРасширенная(ИмяОперации, ТекстИнформации, УровеньЖурналаРегистрации.Информация);
		
	Возврат ОтветXDTO;
	
КонецФункции

// Импорт шаблонов доступа систем
//
// Параметры:
//  ID_System		 - Строка - идентификация по реквизиту ИД_ИнформационнойБазы справочника "СписокИнформационныхБаз1С" 
//  AccessPatterns	 - ОбъектXDTO	 -  см. ОбъектXDTO.AccessControlData.AccessPatterns
// 
// Возвращаемое значение:
//  ОбъектXDTO - см. ОбъектXDTO.AccessControlData.ResponseAccessPatterns
//
Функция PutAccessPatterns(ID_System, AccessPatterns)
	
	// логирование
	ИмяОперации = "Администрирование.УправлениеСетью.УправлениеДоступомПользователей.PutAccessPatterns";
	ТекстИнформации = СтрШаблон(НСтр("ru = 'Начало работы с системой %1'"), ID_System); 
	ОбщегоНазначенияКупоросСервер.ЗаписьЖурналаРегистрацииРасширенная(ИмяОперации, ТекстИнформации, УровеньЖурналаРегистрации.Информация);
	
	ПроверитьБлокировкуИнформационнойБазыДляОбновления();
	
	// - Импорт актуальной таблицы шаблонов доступа системы.
	РезультатИмпорта = УправлениеДаннымиВнешнихСистем.ИмпортШаблоновДоступаВнешнихСистем(ID_System, AccessPatterns);
	
	// - Формирование ответа.
	ОтветXDTO = УправлениеДаннымиВнешнихСистем.ОтветResponseAccessPatterns(РезультатИмпорта);
	
	// логирование
	ТекстИнформации = СтрШаблон(НСтр("ru = 'Конец работы с системой %1'"), ID_System); 
	ОбщегоНазначенияКупоросСервер.ЗаписьЖурналаРегистрацииРасширенная(ИмяОперации, ТекстИнформации, УровеньЖурналаРегистрации.Информация);
	
	Возврат ОтветXDTO;
	
КонецФункции

// Возвращает таблицу прав для обновления из справочника ИменаПользователей
//
// Параметры:
//  ID_System		 - Строка - идентификация по реквизиту ИД_ИнформационнойБазы справочника "СписокИнформационныхБаз1С" 
// 
// Возвращаемое значение:
//  ОбъектXDTO - см. ОбъектXDTO.AccessControlData.ResponseAccessRights 
//
Функция GetAccessRights(ID_System)
	
	// логирование
	ИмяОперации = "Администрирование.УправлениеСетью.УправлениеДоступомПользователей.GetAccessRights";
	ТекстИнформации = СтрШаблон(НСтр("ru = 'Начало работы с системой %1'"), ID_System); 
	ОбщегоНазначенияКупоросСервер.ЗаписьЖурналаРегистрацииРасширенная(ИмяОперации, ТекстИнформации, УровеньЖурналаРегистрации.Информация);
	
	ПроверитьБлокировкуИнформационнойБазыДляОбновления();
	
	// - Получение таблицы прав пользователей.
	ТаблицаПользователей = УправлениеДаннымиВнешнихСистем.ТаблицаПравПользователяДляОбновления(ID_System);
	
	// - Формирование ответа.
	ОтветXDTO = УправлениеДаннымиВнешнихСистем.ОтветResponseAccessRights(ТаблицаПользователей);
	
	// - Снятие признака обновления прав доступа.
	УправлениеДаннымиВнешнихСистем.СнятиеПризнакаОбновленияПрав(ID_System, ТаблицаПользователей);
	
	// логирование
	ТекстИнформации = СтрШаблон(НСтр("ru = 'Конец работы с системой %1'"), ID_System); 
	ОбщегоНазначенияКупоросСервер.ЗаписьЖурналаРегистрацииРасширенная(ИмяОперации, ТекстИнформации, УровеньЖурналаРегистрации.Информация);
	
	Возврат ОтветXDTO;
	
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// Локальные служебные процедуры и функции.

Процедура ПроверитьБлокировкуИнформационнойБазыДляОбновления()
	
	Если ЗначениеЗаполнено(ОбновлениеИнформационнойБазыСлужебный.ИнформационнаяБазаЗаблокированаДляОбновления()) Тогда
		
		ВызватьИсключение НСтр("ru = 'Синхронизация данных временно недоступна в связи с обновлением приложения в Интернете.'");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
