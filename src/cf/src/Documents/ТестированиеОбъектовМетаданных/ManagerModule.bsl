
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция СформироватьКарточкуДокумента(МассивОбъектов, ОбъектыПечати) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТестированиеОбъектовМетаданных.Дата КАК Дата,
	|	ТестированиеОбъектовМетаданных.Номер КАК Номер,
	|	ТестированиеОбъектовМетаданных.ОбъектыНаПроверке.(
	|		НомерСтроки КАК НомерСтроки,
	|		ОбъектМетаданных КАК ОбъектМетаданных
	|	) КАК ОбъектыНаПроверке,
	|	ТестированиеОбъектовМетаданных.ЭтапыПроверки.(
	|		Ссылка КАК Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		ЭтапПроверки КАК ЭтапПроверки,
	|		РезультатЭтапа КАК РезультатЭтапа,
	|		КомментарийРезультата КАК КомментарийРезультата
	|	) КАК ЭтапыПроверки,
	|	ТестированиеОбъектовМетаданных.ОписаниеЗадания КАК ОписаниеЗадания,
	|	ТестированиеОбъектовМетаданных.РезультатПроверки КАК РезультатПроверки
	|ИЗ
	|	Документ.ТестированиеОбъектовМетаданных КАК ТестированиеОбъектовМетаданных
	|ГДЕ
	|	ТестированиеОбъектовМетаданных.Ссылка В(&МассивОбъектов)";
	Запрос.Параметры.Вставить("МассивОбъектов", МассивОбъектов);
	Выборка = Запрос.Выполнить().Выбрать();

	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ТестированиеОбъектовМетаданных_КарточкаДокумента";

	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ТестированиеОбъектовМетаданных.Печать");
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	Шапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьОбъектыНаПроверкеШапка = Макет.ПолучитьОбласть("ОбъектыНаПроверкеШапка");
	ОбластьОбъектыНаПроверке = Макет.ПолучитьОбласть("ОбъектыНаПроверке");
	ОбластьЭтапыПроверкиШапка = Макет.ПолучитьОбласть("ЭтапыПроверкиШапка");
	ОбластьЭтапыПроверки = Макет.ПолучитьОбласть("ЭтапыПроверки");
	ТабДокумент.Очистить();

	ВставлятьРазделительСтраниц = Ложь;
	Пока Выборка.Следующий() Цикл
		
		Если ВставлятьРазделительСтраниц Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;

		ТабДокумент.Вывести(ОбластьЗаголовок);

		Шапка.Параметры.Заполнить(Выборка);
		ТабДокумент.Вывести(Шапка, Выборка.Уровень());

		ТабДокумент.Вывести(ОбластьОбъектыНаПроверкеШапка);
		ВыборкаОбъектыНаПроверке = Выборка.ОбъектыНаПроверке.Выбрать();
		Пока ВыборкаОбъектыНаПроверке.Следующий() Цикл
			ОбластьОбъектыНаПроверке.Параметры.Заполнить(ВыборкаОбъектыНаПроверке);
			ТабДокумент.Вывести(ОбластьОбъектыНаПроверке, ВыборкаОбъектыНаПроверке.Уровень());
		КонецЦикла;
		
		ТабДокумент.Вывести(ОбластьЭтапыПроверкиШапка);
		ВыборкаЭтапыПроверки = Выборка.ЭтапыПроверки.Выбрать();
		Пока ВыборкаЭтапыПроверки.Следующий() Цикл
			ОбластьЭтапыПроверки.Параметры.Заполнить(ВыборкаЭтапыПроверки);
			ТабДокумент.Вывести(ОбластьЭтапыПроверки, ВыборкаЭтапыПроверки.Уровень());
		КонецЦикла;

		ВставлятьРазделительСтраниц = Истина;
		
	КонецЦикла;
	
	Если ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	ТабДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабДокумент;
	
КонецФункции

Процедура СброситьФактТестированияОбъектовМетаданных(Конфигурация) Экспорт

	Выборка = Справочники.ОбъектыМетаданныхКонфигураций.Выбрать(,Конфигурация);
	
	Пока Выборка.Следующий() Цикл
	
		 ОбъектДанных = Выборка.Ссылка.ПолучитьОбъект();
		 ОбъектДанных.Заблокировать();
		 ОбъектДанных.ОбъектПротестирован = Ложь;
		 ОбъектДанных.Записать();
	
	КонецЦикла;	
	
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Сброшены флаги тестирования для конфигурации:  %1.'"), Строка(Конфигурация));
	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Печать карточки задачи
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "КарточкаДокумента";
	КомандаПечати.Представление = НСтр("ru = 'Печать карточки документа'");

КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр).
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной
//                                                            параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной
//                                            параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "КарточкаДокумента") Тогда

		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, 
			"КарточкаДокумента",
			НСтр("ru = 'Печать карточки документа'"),
			СформироватьКарточкуДокумента(МассивОбъектов, ОбъектыПечати));
				
	КонецЕсли;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Печать

#КонецОбласти

#КонецЕсли
