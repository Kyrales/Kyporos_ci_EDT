

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ВыбораГруппыAD_ГруппыПользователейAD" Тогда
		Объект.ПутьКГруппеAD = Параметр;
		ЭтаФорма.Модифицированность = Истина;
		СформироватьСтрокуСоединения();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ГруппаADНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("ДеревоADФормы", ДеревоADФормы);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Справочник.ГруппыПользователейAD.Форма.ВыборГруппыAD", ПараметрыФормы);	
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяГруппыADПриИзменении(Элемент)
	
	СформироватьСтрокуСоединения();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьПользователей(Команда)
	ОбновитьПользователейНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтрокуСоединения(Команда)
	
	СформироватьСтрокуСоединения();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьПользователейНаСервере()
	
	ТаблицаПользователей = РаботаСоСтруктуройКаталоговAD.ЗагрузитьПользователейИзГруппыAD(Объект.СтрокаСоединения);
	
	СтруктураОтбора = Новый Структура("ИмяПользователя", Справочники.ИменаПользователей.ПустаяСсылка());
	ТаблицаПользователей = ОбщегоНазначенияКупоросСервер.УдалитьПустыеСтрокиТаблицыЗначений(ТаблицаПользователей, СтруктураОтбора);
	
	Объект.ПользователиГруппы.Загрузить(ТаблицаПользователей);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьСтрокуСоединения()

	rootDSE = ПолучитьCOMОбъект("LDAP://RootDSE");
	DomainContainer = rootDSE.Get("defaultNamingContext");
	DomainContainer = "<LDAP://" + DomainContainer + ">";
	
	// Пример: СтрокаДляСоединения = "<LDAP://DC=knfmp,DC=net>;(&(objectClass=user)(memberOf:1.2.840.113556.1.4.1941:=CN=GSG-1C_KP-USERS,OU=User Groups,OU=Groups,OU=1C_KP,OU=Services,OU=Enterprise,DC=knfmp,DC=net));name,AdsPath; SubTree";
	Объект.СтрокаСоединения = СтрШаблон(НСтр("ru = '%1;(&(objectClass=user)(memberOf:1.2.840.113556.1.4.1941:=CN=%2,%3));ADsPath, Name, DisplayName, Mail, Company, department, distinguishedName, telephoneNumber, homephone, sAMAccountName; SubTree'"), 
		DomainContainer, Объект.ИмяГруппыAD, Объект.ПутьКГруппеAD)
	
КонецПроцедуры 

#КонецОбласти
