
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов	

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РольПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ОписаниеИстории) Тогда
		Возврат;	
	КонецЕсли;
	
	ОбновитьПользовательскуюИсторию();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполненаПриИзменении(Элемент)
	
	Объект.ДатаВыполнения = ?(Объект.Выполнена, ОбщегоНазначенияКлиент.ДатаСеанса(), '00010101');
	
КонецПроцедуры

&НаСервере
Процедура ВладелецПриИзмененииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.Владелец) Тогда
		Объект.Проект = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Владелец, "Владелец.Проект");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	ВладелецПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьИсторию(Команда)
	
	ОбновитьПользовательскуюИсторию();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьПользовательскуюИсторию()

	Если Не ЗначениеЗаполнено(Объект.Роль) Тогда
		Возврат;		
	КонецЕсли;
	
	СтрокаОбработки = НСтр("ru = 'Как %1 хочу %2, чтобы (цель)'");
	Объект.ОписаниеИстории = СтрШаблон(СтрокаОбработки, Объект.Роль, Объект.Наименование);

КонецПроцедуры 

#КонецОбласти

