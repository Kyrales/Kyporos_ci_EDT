
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбновитьСписокДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_СпискаИнформационныхБаз1С" Тогда
	
		ОбновитьСписокДанных();
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьТаблицуИнформационныхБазПользователя(СписокОбъектовДоступа)

	 Запрос = Новый Запрос;
	 Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	                |	СписокИнформационныхБаз1ССписокПравДоступа.Ссылка,
	                |	СписокИнформационныхБаз1ССписокПравДоступа.Ссылка.Наименование,
	                |	СписокИнформационныхБаз1ССписокПравДоступа.Ссылка.Используется
	                |ИЗ
	                |	Справочник.СписокИнформационныхБаз1С.СписокПравДоступа КАК СписокИнформационныхБаз1ССписокПравДоступа
	                |ГДЕ
	                |	СписокИнформационныхБаз1ССписокПравДоступа.ОбъектДоступа В(&СписокОбъектовДоступа)
	                |	И НЕ СписокИнформационныхБаз1ССписокПравДоступа.Ссылка.ПометкаУдаления";
	 
	 Запрос.УстановитьПараметр("СписокОбъектовДоступа", СписокОбъектовДоступа);
	 
	 Результат = Запрос.Выполнить();
	 Возврат Результат.Выгрузить();
	 
КонецФункции // ПолучитьТаблицуИнформационныхБазПользователя()

&НаКлиенте
Процедура СписокИнформационныхБазВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	 ПоказатьЗначение(,Элемент.ТекущиеДанные.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокДанных() 
	
	Если Пользователь.Пустая() 
		И ТипЗнч(Параметры.ВладелецФайла) = Тип("СправочникСсылка.ИменаПользователей") Тогда
		Пользователь = Параметры.ВладелецФайла;	
	КонецЕсли;
	
	СписокОбъектовДоступа.Вставить(0,Пользователь);
	
	// определяем список объектов доступа по группе пользователя
	МассивГруппПользователя = РаботаСЗадачами.ПолучитьСписокГруппПользователя(Справочники.Пользователи.НайтиПоНаименованию(Пользователь.Наименование));
	Для каждого ТекСтрокаГрупп Из МассивГруппПользователя Цикл
		СписокОбъектовДоступа.Вставить(0,ТекСтрокаГрупп.Ссылка);
	КонецЦикла;
	
	// определяем список объектов доступа по подразделению
	ТекущееПодразделение = Пользователь.Подразделение;	
	СписокОбъектовДоступа.Вставить(0,ТекущееПодразделение);
	Пока Не ТекущееПодразделение.Родитель.Пустая() Цикл
		ТекущееПодразделение = ТекущееПодразделение.Родитель;
		СписокОбъектовДоступа.Вставить(0,ТекущееПодразделение);
	КонецЦикла;
	
	СписокИнформационныхБаз.Загрузить(ПолучитьТаблицуИнформационныхБазПользователя(СписокОбъектовДоступа));
		
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьФайлНастроекСпискаБазПользователей(Команда)
	
	ОбновитьФайлНастроекСпискаБазПользователейНаСервере(Пользователь);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбновитьФайлНастроекСпискаБазПользователейНаСервере(Пользователь)
	
	Если Не ЗначениеЗаполнено(Пользователь) Тогда
		Возврат;
	КонецЕсли;
	
	РаботаСоСтруктуройКаталоговAD.ОбновитьФайлНастроекСпискаБазПользователя(Пользователь);
	
	ТекстСообщения = СтрШаблон(НСтр("ru = 'Обновлен файл настроек баз 1С пользователя:  %1.'"), Пользователь);
	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	
КонецПроцедуры

#КонецОбласти
