
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Источник") Тогда
		Источник = Параметры.Источник;
	КонецЕсли;	
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(ПриемникСерверSQL) Тогда
		ЗаполнитьПапкуКБазе(ПриемникСерверSQL);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПриемникСерверSQLПриИзменении(Элемент)
	
	ЗаполнитьПапкуКБазе(ПриемникСерверSQL);
	
КонецПроцедуры

&НаКлиенте
Процедура ПапкаКБазеДанныхПриИзменении(Элемент)
	
	Если ПапкаКБазеДанных<>"" Тогда
		ПапкаКБазеДанных = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ПапкаКБазеДанных);
		ПапкаКБазеДанных = Лев(ПапкаКБазеДанных,СтрДлина(ПапкаКБазеДанных)-1);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПапкаКБазеДанныхНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПапкаКБазеДанныхНачалоВыбораЗавершение", ЭтотОбъект);
	ФайловаяСистемаКлиент.ВыбратьКаталог(ОписаниеОповещения, НСтр("ru = 'Выберите каталог'"), ПапкаКБазеДанных);
		
КонецПроцедуры

&НаКлиенте
Процедура ПапкаКБазеДанныхНачалоВыбораЗавершение(ИмяКаталога, Параметры) Экспорт
	
	Если Не ЗначениеЗаполнено(ИмяКаталога) Тогда
		Возврат;
	КонецЕсли;
	
	ПапкаКБазеДанных = ИмяКаталога;
	
КонецПроцедуры 

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьВосстановлениеБазыДанных(Команда)
	
	ВыполнитьВосстановлениеБазыДанныхСервер();	
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьВосстановлениеБазыДанныхСервер()
	
	Если Не ЗначениеЗаполнено(Источник) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не заполнен Источник.'"));
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ПриемникСерверSQL) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не заполнен Приемник - Сервер SQL.'"));
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ПриемникБазаДанных) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не заполнен Приемник - База данных.'"));
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ПапкаКБазеДанных) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не заполнен Приемник - Папка к базе данных SQL.'"));
		Возврат;
	КонецЕсли;
	
	НастройкаДляВосстановления = СоздатьОбразНастройкиПоЭлементуСервер();
	
	НастройкаДляВосстановления.Вставить("ИмяБазыНаСервереSQL_Приемник",ПриемникБазаДанных);
	НастройкаДляВосстановления.Вставить("ВосстановлениеПустойБазы",ВосстановлениеПустойБазы);
	
	ЗаполнитьЗначенияСвойств(НастройкаДляВосстановления,ПриемникСерверSQL,,"Ссылка");
	
	НастройкаДляВосстановления.ПапкаКБазамПоУмолчанию = ПапкаКБазеДанных; 	
	
	Если СоздатьАктуальнуюКопиюБазыИсточника Тогда
		УправлениеРезервнымКопированиемВызовСервера.ВыполнитьРезервноеКопированиеПоНастройке(УправлениеРезервнымКопированиемВызовСервера.СоздатьОбразНастройкиПоЭлементу(Источник),Истина);
		РаботаСоСтруктуройКаталоговAD.Sleep(40); // Перед восстановлением базы из архива ставить паузу. Причина: иногда возникает ошибка восстановления, скорей всего SQL не успевает переключится между заданием создания копии на новое.	
	КонецЕсли;
	
	УправлениеРезервнымКопированиемВызовСервера.ВыполнитьВосстановлениеПоНастройке(НастройкаДляВосстановления,Истина); 
	 
КонецПроцедуры // ВыполнитьВосстановлениеБазыДанныхСервер()

&НаКлиенте
Процедура ЗаполнитьПриемникИсточником(Команда)
	
	ЗаполнитьПриемникИсточникомСервер();	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПриемникИсточникомСервер() 
	
	ПриемникСерверSQL = Источник.СерверSQL;
	ПриемникБазаДанных = Источник.ИмяБазыНаСервереSQL;
	ПапкаКБазеДанных = "";
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СоздатьОбразНастройкиПоЭлементуСервер() 
	
	Возврат УправлениеРезервнымКопированиемВызовСервера.СоздатьОбразНастройкиПоЭлементу(Источник);
	
КонецФункции

&НаСервере
Процедура ЗаполнитьПапкуКБазе(ПриемникСерверSQL)

	ПапкаКБазеДанных = ПриемникСерверSQL.ПапкаКБазамПоУмолчанию;	

КонецПроцедуры // ЗаполнитьПапкуКБазе()

#КонецОбласти




