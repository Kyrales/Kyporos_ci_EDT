
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнформацияПоСлужебнойПапке = СтрШаблон(НСтр("ru = 'Путь к общей служебной папке: %1'"), Справочники.СтруктураПредприятия.СлужебнаяПапка.ПутьККаталогуПодразделения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьФайлыНастроекСпискаБазПользователей(Команда)
	
	Если Элементы.Список.ТекущиеДанные <> Неопределено  Тогда 
		Для каждого ТекСтрока Из Элементы.Список.ВыделенныеСтроки Цикл
			
			Результат = Ложь;
			ОбновитьФайлыНастроекСпискаБазПользователейСервер(ТекСтрока);
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Обновление настроек списка баз пользователей по элементу %1 завершено.'"),ТекСтрока);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьФайлыНастроекСпискаБазПользователейСервер(ИнформационнаяБазаЗапуска) 
	
	Если ИнформационнаяБазаЗапуска.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	МассивПользователей = ИнформационнаяБазаЗапуска.СписокПравДоступа.ВыгрузитьКолонку("ОбъектДоступа");	
	
	РаботаСоСтруктуройКаталоговAD.ОбновитьФайлыНастроекСпискаБазПользователейПоСписку(МассивПользователей);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьПредприятие(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущаяСтрока;
	
	ФайловаяСистемаКлиент.ЗапуститьПрограмму(ПолучитьКомандуОткрытияВРежимеПредприятия(ТекущиеДанные));
	
КонецПроцедуры

&НаСервере
Функция ПолучитьКомандуОткрытияВРежимеПредприятия(ИнформационнаяБаза)
	
	Возврат Справочники.СписокИнформационныхБаз1С.ПолучитьКомандуОткрытияВРежимеПредприятия(ИнформационнаяБаза);
	
КонецФункции

&НаКлиенте
Процедура ЗапуститьКонфигуратор(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущаяСтрока;
	
	ФайловаяСистемаКлиент.ЗапуститьПрограмму(ПолучитьКомандуОткрытияВРежимеКонфигуратора(ТекущиеДанные));

КонецПроцедуры

&НаСервере
Функция ПолучитьКомандуОткрытияВРежимеКонфигуратора(ИнформационнаяБаза)
	
	Возврат Справочники.СписокИнформационныхБаз1С.ПолучитьКомандуОткрытияВРежимеКонфигуратора(ИнформационнаяБаза);
	
КонецФункции

&НаКлиенте
Процедура ЗапуститьПредприятиеВОбычномРежиме(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущаяСтрока;
	
	ФайловаяСистемаКлиент.ЗапуститьПрограмму(ПолучитьКомандуОткрытияВРежимеПредприятия(ТекущиеДанные)+" /RunModeOrdinaryApplication");

КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьПредприятиеВУправляемомРежиме(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущаяСтрока;
	
	ФайловаяСистемаКлиент.ЗапуститьПрограмму(ПолучитьКомандуОткрытияВРежимеПредприятия(ТекущиеДанные)+" /RunModeManagedApplication");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьРезервноеКопированиеБазыДанных(Команда)
	
	Если Элементы.Список.ТекущиеДанные <> Неопределено  Тогда 
		Для каждого ТекСтрока Из Элементы.Список.ВыделенныеСтроки Цикл
			
			УправлениеРезервнымКопированиемВызовСервера.ВыполнитьРезервноеКопированиеПоНастройке(УправлениеРезервнымКопированиемВызовСервера.СоздатьОбразНастройкиПоЭлементу(ТекСтрока),Истина);
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВосстановленияБазыДанных(Команда)
	
	Если Элементы.Список.ТекущиеДанные = Неопределено  Тогда 
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = Элементы.Список.ТекущаяСтрока;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Источник",  ТекущаяСтрока);
	
	ОткрытьФорму("Справочник.СписокИнформационныхБаз1С.Форма.ВосстановлениеИнформационнойБазы", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

