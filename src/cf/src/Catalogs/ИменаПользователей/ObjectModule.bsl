#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДатаИзменения = ТекущаяДатаСеанса();
	
	ОбновитьБазыДляОбновленияПравПередЗаписью();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьБазыДляОбновленияПравПередЗаписью()
	
	ТаблицаИСДоИзменения 	= ТаблицаИнформационныхБазПоШаблонамДоступа(Ссылка.ПраваПользователя.ВыгрузитьКолонку("ШаблонДоступа"));
	ТаблицаИСПослеИзменения = ТаблицаИнформационныхБазПоШаблонамДоступа(ПраваПользователя.ВыгрузитьКолонку("ШаблонДоступа"));
	
	Изменения = ОбщегоНазначенияКупоросСервер.РазницаТаблицЗначений(ТаблицаИСДоИзменения, ТаблицаИСПослеИзменения);
	
	Для каждого ТекСтрока Из Изменения Цикл
		
		Справочники.ИменаПользователей.ДобавитьБазуДляОбновленияПрав(ЭтотОбъект, ТекСтрока.ИнформационнаяБаза);
				
	КонецЦикла;
	
КонецПроцедуры 

Функция ТаблицаИнформационныхБазПоШаблонамДоступа(СписокШаблоновДоступа)

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ШаблоныДоступаПользователей.ИнформационнаяБаза КАК ИнформационнаяБаза,
	               |	ШаблоныДоступаПользователей.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.ШаблоныДоступаПользователей КАК ШаблоныДоступаПользователей
	               |ГДЕ
	               |	ШаблоныДоступаПользователей.Ссылка В(&СписокШаблонДоступа)";
	
	Запрос.УстановитьПараметр("СписокШаблонДоступа", СписокШаблоновДоступа);
	
	Результат = Запрос.Выполнить();
	ТаблицаДанных = Результат.Выгрузить();
	
	Возврат ТаблицаДанных;

КонецФункции

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
