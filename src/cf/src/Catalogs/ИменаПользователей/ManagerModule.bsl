#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция ищет и создает элемент справочника (если не найден пользователь)
//
// Параметры:
//  ПараметрыЗаполнения	 - Структура:
//		* Email - Строка 
//		* Сотрудник - Строка - поиск по наименованию 
//		* Аккаунт - Строка - поиск по реквизиту УчетнаяЗаписьWindows 
//  ТолькоПоиск			 - Булево 
// 
// Возвращаемое значение:
//   СправочникСсылка.ИменаПользователей
//
Функция НайтиСоздатьИмяПользователя(ПараметрыЗаполнения, ТолькоПоиск = Ложь) Экспорт
	
	Результат = Справочники.ИменаПользователей.ПустаяСсылка();
	
	Если ПараметрыЗаполнения.Свойство("Email") Тогда
		Результат = Справочники.ИменаПользователей.НайтиПоРеквизиту("Email",ПараметрыЗаполнения.Email);
	КонецЕсли;	
	
	Если Не ЗначениеЗаполнено(Результат)
		И ПараметрыЗаполнения.Свойство("Сотрудник") Тогда
		Результат = Справочники.ИменаПользователей.НайтиПоНаименованию(ПараметрыЗаполнения.Сотрудник,Истина);
	КонецЕсли;	
	
	Если Не ЗначениеЗаполнено(Результат)
		И ПараметрыЗаполнения.Свойство("Аккаунт") Тогда
		Результат = Справочники.ИменаПользователей.НайтиПоРеквизиту("УчетнаяЗаписьWindows",ПараметрыЗаполнения.Аккаунт);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Результат)
		Или ТолькоПоиск Тогда
		Возврат Результат;
	КонецЕсли;
	
	// создаем новый элемент справочника
	ОбъектИменаПользователей = Справочники.ИменаПользователей.СоздатьЭлемент();
	ЗаполнитьЗначенияСвойств(ОбъектИменаПользователей,ПараметрыЗаполнения);	
	ОбъектИменаПользователей.Записать();
	
	Результат = ОбъектИменаПользователей.Ссылка;
	
	Возврат Результат;
	
КонецФункции // НайтиСоздатьИмяПользователя()

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Добавить или обновить базу для обновления прав пользователя
//
// Параметры:
//  ОбъектИменаПользователей - СправочникОбъект.ИменаПользователей - объект для обновления
//  ИнформационнаяБаза		 - СправочникСсылка.СписокИнформационныхБаз1С - база для обновления прав
//
Процедура ДобавитьБазуДляОбновленияПрав(ОбъектИменаПользователей, ИнформационнаяБаза) Экспорт
	
	СтрокаТабличнойЧасти = ОбъектИменаПользователей.ИнформационныеБазыДляОбновленияПрав.Найти(ИнформационнаяБаза, "ИнформационнаяБаза");
	Если СтрокаТабличнойЧасти = Неопределено Тогда
		СтрокаТабличнойЧасти = ОбъектИменаПользователей.ИнформационныеБазыДляОбновленияПрав.Добавить();
		СтрокаТабличнойЧасти.ИнформационнаяБаза = ИнформационнаяБаза;
	КонецЕсли;
	
	СтрокаТабличнойЧасти.ОбновитьПрава = Истина;

КонецПроцедуры 

#КонецОбласти

#КонецЕсли
