
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ЭтоГруппа Тогда
		Возврат;	
	КонецЕсли;
	
	ВидыКлавиатуры = Перечисления.ТелеграмВидыКлавиатуры;
	Если ВидКлавиатуры = ВидыКлавиатуры.Контекстная Тогда
		
		Для Каждого СтрокаКнопка Из Кнопки Цикл
			
			Если Не ЗначениеЗаполнено(СтрокаКнопка.ДанныеОтправки) 
				И Не ЗначениеЗаполнено(СтрокаКнопка.URL) Тогда
				
				ТекстСообщения = СтрШаблон(НСтр("ru = 'В строке %1 табличной части ""Кнопки"" не заполнены ни данные отправки, ни URL.'"), СтрокаКнопка.НомерСтроки);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
				
				Отказ = Истина;
				
			ИначеЕсли ЗначениеЗаполнено(СтрокаКнопка.ДанныеОтправки) 
				И ЗначениеЗаполнено(СтрокаКнопка.URL) Тогда
				
				ТекстСообщения = СтрШаблон(НСтр("ru = 'В строке %1 табличной части ""Кнопки"" заполнены и данные отправки, и URL (нужно что-то одно).'"), СтрокаКнопка.НомерСтроки);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
				
				Отказ = Истина;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
