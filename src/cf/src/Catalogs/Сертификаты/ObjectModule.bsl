
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ДатаИзменения = ТекущаяДатаСеанса();
	
	Если Не ЗначениеЗаполнено(ДатаСоздания) Тогда
		ДатаСоздания = ТекущаяДатаСеанса();
	КонецЕсли;
	
	// ИС строкой
	МассивИССистем = ИнформационныеСистемы.ВыгрузитьКолонку("ИнформационнаяСистема");
	ИнформационныеСистемыСтрока = СтрСоединить(МассивИССистем, ";");
	
	// Получение новой ссылки для нового объекта.
	СсылкаНаСертификат = ОбщегоНазначенияКупоросСервер.СсылкаНаНезаписанныйОбъект(ЭтотОбъект, Ссылка);	
	
	// Установка напоминаний.
	НапоминанияМожноУстановить = Не БезНапоминаний И Не НапоминанияУстановлены;
	
	Если НапоминанияМожноУстановить 
		И СтатусСертификата = Перечисления.СтатусыСертификатов.Активный 
		И ЗначениеЗаполнено(СрокДействия) Тогда
		
		// Дополнительное свойство для отрабоки в событии ПриЗаписи.
		ДополнительныеСвойства.Вставить("УстановитьНапоминания", Истина);
		
		НапоминанияУстановлены = Истина;
		
	ИначеЕсли СтатусСертификата = Перечисления.СтатусыСертификатов.Неактуален
		Или БезНапоминаний Тогда
		
		// удаление старых напоминаний						
		Справочники.Сертификаты.УдалитьНапоминанияПоСертификату(СсылкаНаСертификат);
		
	Иначе
		
		// Не требуется обработка.
		
	КонецЕсли;
	
КонецПроцедуры
 
Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("УстановитьНапоминания")
		И ДополнительныеСвойства.УстановитьНапоминания Тогда
		
		СписокПользователей = Ответственные.ВыгрузитьКолонку("Пользователь");
		
		ТекстНапоминания = СтрШаблон(НСтр("ru = 'Сертификат скоро просрочится☝. Необходимо произвести процедуру продления.
                                           |Сертификат: %1 . ⏳Действует до %2
                                           |Владелец сертификата: %4
                                           |Рабочее место: %5
                                           |
                                           |Список ответственных: %6
                                           |Комментарий: %7
                                           |Ссылка на сертификат в 1С: %3'"), 
								Наименование, 
								Формат(СрокДействия, "ДФ=dd.MM.yyyy"), 
								ПолучитьНавигационнуюСсылку(Ссылка),
		                        ВладелецСертификата,
								РабочееМесто,
								СтрСоединить(СписокПользователей, ", "),
								Комментарий);	
								
		// удаление старых напоминаний						
		Справочники.Сертификаты.УдалитьНапоминанияПоСертификату(Ссылка);
		
		// установка новых напоминаний
		Справочники.Сертификаты.УстановитьНапоминаниеПоСертификатуОтветственным(Ссылка, СрокДействия, СписокПользователей, ТекстНапоминания, "Месяц");
		Справочники.Сертификаты.УстановитьНапоминаниеПоСертификатуОтветственным(Ссылка, СрокДействия, СписокПользователей, ТекстНапоминания, "Неделя");
		Справочники.Сертификаты.УстановитьНапоминаниеПоСертификатуОтветственным(Ссылка, СрокДействия, СписокПользователей, ТекстНапоминания, "День");
		
		ДополнительныеСвойства.УстановитьНапоминания = Ложь;
		
	КонецЕсли;	
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)

	ТекущийОтветственный = Пользователи.ТекущийПользователь();
	
	НовОтветственный = Ответственные.Добавить();
	НовОтветственный.Пользователь = ТекущийОтветственный;
	
	Ответственный = ТекущийОтветственный;
	Автор = ТекущийОтветственный;
	
	СтандартныеПодсистемыКупорос.ПриВводеНовогоЗаполнитьОрганизацию(ЭтотОбъект);
		
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ИсключаемыеРеквизиты = Новый Массив;
	
	Если СтатусСертификата <> Перечисления.СтатусыСертификатов.Активный Тогда
		ИсключаемыеРеквизиты.Добавить("СрокДействия");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, ИсключаемыеРеквизиты);
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли