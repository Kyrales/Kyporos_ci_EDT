
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает расчитанный период спринта относительно типа спринта и даты периода
// 
// Параметры:
//  ТипСпринта - ПеречислениеСсылка.ТипыСпринтов
//  ДатаПериода - Дата
//  КолВоСпринтовОтДатыПериода - Число - Количество спринтов для прибавления/отнимания. По-умолчанию = 0
//  ДатаПериодаПоБазовойДате - Булево - Если Истина, то расчитывает дату окончания от ДатаПериода
// 
// Возвращаемое значение:
//  Структура:
// * ДатаНачалаСпринта - Дата
// * ДатаОкончанияСпринта - Дата
// * КолВоДнейСпринт - Число
// * КолВоДнейОсталось - Число
// * КолВоДнейПрошло - Число
Функция ПериодСпринта(ТипСпринта, ДатаПериода, КолВоСпринтовОтДатыПериода = 0, ДатаПериодаПоБазовойДате = Ложь) Экспорт

	Результат = Новый Структура;
	Результат.Вставить("ДатаНачалаСпринта", '00010101');
	Результат.Вставить("ДатаОкончанияСпринта", '00010101');
	Результат.Вставить("КолВоДнейСпринт", 0);
	Результат.Вставить("КолВоДнейОсталось", 0);
	Результат.Вставить("КолВоДнейПрошло", 0);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(&Период, Месяц) КАК ДатаНачалаСпринта,
	|	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Период, Месяц), Месяц, &КолЭлементовДляПрибавления) КАК ДатаОкончанияСпринта
	|ПОМЕСТИТЬ ВТ_ДатыПериодов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДатыПериодов.ДатаНачалаСпринта,
	|	ВТ_ДатыПериодов.ДатаОкончанияСпринта,
	|	РАЗНОСТЬДАТ(ВТ_ДатыПериодов.ДатаНачалаСпринта, ВТ_ДатыПериодов.ДатаОкончанияСпринта, ДЕНЬ) КАК КолВоДнейСпринт,
	|	РАЗНОСТЬДАТ(&ТекущаяДата, ВТ_ДатыПериодов.ДатаОкончанияСпринта, ДЕНЬ) КАК КолВоДнейОсталось,
	|	РАЗНОСТЬДАТ(ВТ_ДатыПериодов.ДатаНачалаСпринта, &ТекущаяДата, ДЕНЬ) КАК КолВоДнейПрошло
	|ИЗ
	|	ВТ_ДатыПериодов КАК ВТ_ДатыПериодов";

	Запрос.УстановитьПараметр("Период", НачалоДня(ДатаПериода));
	Запрос.УстановитьПараметр("ТекущаяДата", ДатаПериода);
	
	Если ДатаПериодаПоБазовойДате Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "НАЧАЛОПЕРИОДА(&Период, Месяц)",
			"НАЧАЛОПЕРИОДА(&Период, День)");
	КонецЕсли;
	
	ПараметрыСпринта = РазмерностьСпринта(ТипСпринта); // определяем размерность спринта и кол-во элементов для прибавления
	РазмерностьСпринта = ПараметрыСпринта.РазмерностьСпринта;
	КолЭлементовДляПрибавления = ПараметрыСпринта.КолЭлементовДляПрибавления;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "Месяц", РазмерностьСпринта);
	
	Запрос.УстановитьПараметр("КолЭлементовДляПрибавления", КолЭлементовДляПрибавления);
	
	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		ЗаполнитьЗначенияСвойств(Результат, ВыборкаДетальныеЗаписи);
		
		Результат.ДатаОкончанияСпринта = НачалоДня(Результат.ДатаОкончанияСпринта - 1);
		
		Если КолВоСпринтовОтДатыПериода <> 0 Тогда

			Результат.ДатаНачалаСпринта = ОбщегоНазначенияКупоросСервер.ДобавитьКДате(Результат.ДатаНачалаСпринта,
					КолВоСпринтовОтДатыПериода * КолЭлементовДляПрибавления, РазмерностьСпринта);
			Результат.ДатаОкончанияСпринта = ОбщегоНазначенияКупоросСервер.ДобавитьКДате(Результат.ДатаОкончанияСпринта,
					КолВоСпринтовОтДатыПериода * КолЭлементовДляПрибавления, РазмерностьСпринта);
	
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

// Последний спринт команды.
// 
// Параметры:
//  КомандаСпринта - СправочникСсылка.ГруппыПользователей
//  ТекущаяСсылка - СправочникСсылка.Спринты - Если указан, то будет игнорировать при поиске 
// 
// Возвращаемое значение:
//  Структура - Последний спринт команды:
// * Найден - Булево - Истина, если найден
// * Ссылка - СправочникСсылка.Спринты
// * ДатаНачала - Дата
// * ДатаОкончания - Дата
// * ТипСпринта - Перечисления.ТипыСпринтов
Функция ПоследнийСпринтКоманды(КомандаСпринта, ТекущаяСсылка = Неопределено) Экспорт
	
	РезультатСпринт = Новый Структура();
	РезультатСпринт.Вставить("Найден", Ложь);
	РезультатСпринт.Вставить("Ссылка", Справочники.Спринты.ПустаяСсылка());
	РезультатСпринт.Вставить("ДатаНачала", '00010101');
	РезультатСпринт.Вставить("ДатаОкончания", '00010101');
	РезультатСпринт.Вставить("ТипСпринта", Перечисления.ТипыСпринтов.ПустаяСсылка());
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1 РАЗРЕШЕННЫЕ
		|	Спринты.Ссылка,
		|	Спринты.КомандаСпринта,
		|	Спринты.ДатаНачала КАК ДатаНачала,
		|	Спринты.ДатаОкончания,
		|	Спринты.Ответственный,
		|	Спринты.ТипСпринта,
		|	ИСТИНА КАК Найден
		|ИЗ
		|	Справочник.Спринты КАК Спринты
		|ГДЕ
		|	НЕ Спринты.ПометкаУдаления
		|	И Спринты.КомандаСпринта = &КомандаСпринта
		|	И Спринты.Ссылка <> &ТекущаяСсылка
		|УПОРЯДОЧИТЬ ПО
		|	ДатаНачала УБЫВ";
	
	Запрос.УстановитьПараметр("КомандаСпринта", КомандаСпринта);
	Запрос.УстановитьПараметр("ТекущаяСсылка", ТекущаяСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		ЗаполнитьЗначенияСвойств(РезультатСпринт, ВыборкаДетальныеЗаписи);
		
	КонецЦикла;
	
	Возврат РезультатСпринт;
	
КонецФункции
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РазмерностьСпринта(ТипСпринта)

	Если ТипСпринта = Перечисления.ТипыСпринтов.Месяц Или ТипСпринта = Перечисления.ТипыСпринтов.ДваМесяца
		Или ТипСпринта = Перечисления.ТипыСпринтов.ТриМесяца Тогда
		
		РазмерностьСпринта = "Месяц";

		Если ТипСпринта = Перечисления.ТипыСпринтов.ДваМесяца Тогда
			КолЭлементовДляПрибавления = 2;
		ИначеЕсли ТипСпринта = Перечисления.ТипыСпринтов.ТриМесяца Тогда
			КолЭлементовДляПрибавления = 3;
		Иначе
			КолЭлементовДляПрибавления = 1;
		КонецЕсли;
	
	Иначе
		
		РазмерностьСпринта = "Неделя";
		
		Если ТипСпринта = Перечисления.ТипыСпринтов.ДвеНедели Тогда
			КолЭлементовДляПрибавления = 2;
		ИначеЕсли ТипСпринта = Перечисления.ТипыСпринтов.ТриНедели Тогда
			КолЭлементовДляПрибавления = 3;
		ИначеЕсли ТипСпринта = Перечисления.ТипыСпринтов.ЧетыреНедели Тогда
			КолЭлементовДляПрибавления = 4;
		Иначе
			КолЭлементовДляПрибавления = 1;
		КонецЕсли;

	КонецЕсли;
	
	ДанныеРезультата = Новый Структура;
	ДанныеРезультата.Вставить("РазмерностьСпринта", РазмерностьСпринта);
	ДанныеРезультата.Вставить("ТипСпринта", ТипСпринта);
	ДанныеРезультата.Вставить("КолЭлементовДляПрибавления", КолЭлементовДляПрибавления);
	
	Возврат ДанныеРезультата;
	
КонецФункции // РазмерностьСпринта()

#КонецОбласти

#КонецЕсли