#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Ответственный = Пользователи.ТекущийПользователь();

КонецПроцедуры

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	// Проверить на дубль по: Команда спринта, Дата начала.
	Если ЕстьДубльСпринта() Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Уже есть созданный ранее спринт с такой командой.'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли; 
		
	// Формирование наименования.
	СтрокаДляНаименования = НСтр("ru = '%1-[%2]-[%3]'");
	СтрокаКомандаСпринта = ?(ЗначениеЗаполнено(КомандаСпринта), КомандаСпринта, "Команда не задана");
	Наименование = СтрШаблон(СтрокаДляНаименования, Формат(ДатаНачала,"ДФ=yyyy-MM-dd;"), ТипСпринта, СтрокаКомандаСпринта);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЕстьДубльСпринта()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Спринты.Ссылка,
		|	Спринты.ДатаНачала,
		|	Спринты.КомандаСпринта
		|ИЗ
		|	Справочник.Спринты КАК Спринты
		|ГДЕ
		|	((Спринты.ДатаНачала = &ДатаНачала
		|	ИЛИ Спринты.ДатаНачала МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	ИЛИ Спринты.ДатаОкончания МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	ИЛИ Спринты.ДатаНачала <= &ДатаНачала
		|	И Спринты.ДатаОкончания >= &ДатаОкончания)
		|	И Спринты.КомандаСпринта = &КомандаСпринта
		|	И Спринты.Ссылка <> &Ссылка
		|	И НЕ Спринты.ПометкаУдаления)";
	
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("КомандаСпринта", КомандаСпринта);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Возврат ВыборкаДетальныеЗаписи.Количество() > 0;
	
КонецФункции

#КонецОбласти

#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли