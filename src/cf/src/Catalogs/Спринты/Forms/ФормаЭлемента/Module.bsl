
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КомандаСпринтаПриИзменении(Элемент)
	КомандаСпринтаПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура КомандаСпринтаПриИзмененииНаСервере()
	
	Если Не ЗначениеЗаполнено(Объект.КомандаСпринта) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) И 
		(ЗначениеЗаполнено(Объект.ТипСпринта)  
			Или ЗначениеЗаполнено(Объект.ДатаНачала))  Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеПоследнегоСпринта = Справочники.Спринты.ПоследнийСпринтКоманды(ОБъект.КомандаСпринта, Объект.Ссылка);
	
	Объект.ТипСпринта = ДанныеПоследнегоСпринта.ТипСпринта;
	Объект.ДатаНачала = ДанныеПоследнегоСпринта.ДатаНачала;
	Объект.ДатаОкончания = ДанныеПоследнегоСпринта.ДатаОкончания;
	
	ОбновитьДанныеПериодаСпринта(Объект.ДатаНачала, 1);

КонецПроцедуры

&НаКлиенте
Процедура ТипСпринтаПриИзменении(Элемент)
	
	ОбновитьДанныеПериодаСпринта(ОбщегоНазначенияКлиент.ДатаСеанса(), 0);		
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОбновитьДанныеПериодаСпринта(Объект.ДатаНачала, Направление);		
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаПриИзменении(Элемент)
	
	ОбновитьДанныеПериодаСпринта(Объект.ДатаНачала, 0, Истина);		
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обновить данные периода спринта.
// 
// Параметры:
//  ДатаНачальная - Дата - дата по которой будет определяться период спринта
//  Направление - Число - направление движения спринта: 0, -1, 1. 
//  ДатаПериодаПоБазовойДате - Булево - Если Истина, то расчитывает дату окончания от ДатаПериода
&НаСервере
Процедура ОбновитьДанныеПериодаСпринта(Знач ДатаНачальная, Направление, ДатаПериодаПоБазовойДате = Ложь)
	
	Если Не ЗначениеЗаполнено(Объект.ТипСпринта) Тогда
		Возврат;	
	КонецЕсли;
	
	ДанныеПоПериодам = Справочники.Спринты.ПериодСпринта(Объект.ТипСпринта, ДатаНачальная, Направление, ДатаПериодаПоБазовойДате);
	
	Объект.ДатаНачала = ДанныеПоПериодам.ДатаНачалаСпринта;
	Объект.ДатаОкончания = ДанныеПоПериодам.ДатаОкончанияСпринта;
	
КонецПроцедуры

#КонецОбласти