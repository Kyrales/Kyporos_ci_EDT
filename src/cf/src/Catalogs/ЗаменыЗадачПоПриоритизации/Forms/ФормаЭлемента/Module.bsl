
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АдресТаблицаОтобранныхНаЗаменуЗадач") Тогда
	
		ТаблицаОтобранныхНаЗаменуЗадач = ПолучитьИзВременногоХранилища(Параметры.АдресТаблицаОтобранныхНаЗаменуЗадач); 
		
		СтарыеЗадачиДляЗагрузки = ТаблицаОтобранныхНаЗаменуЗадач.Скопировать(Новый Структура("Распределена", Истина),"Задача, Трудозатраты, УчитыватьТолькоВремяНаОценку");
		Объект.СтарыеЗадачи.Загрузить(СтарыеЗадачиДляЗагрузки);
		
		НовыеЗадачиДляЗагрузки = ТаблицаОтобранныхНаЗаменуЗадач.Скопировать(Новый Структура("Распределена", Ложь),"Задача, Трудозатраты, УчитыватьТолькоВремяНаОценку");
		Объект.НовыеЗадачи.Загрузить(НовыеЗадачиДляЗагрузки);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	АдресСтарыеЗадачи = ПоместитьВоВременноеХранилище(Объект.СтарыеЗадачи.Выгрузить(), УникальныйИдентификатор);
	АдресНовыеЗадачи  = ПоместитьВоВременноеХранилище(Объект.НовыеЗадачи.Выгрузить(), УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПараметрыДляОповещения = Новый Структура;
	ПараметрыДляОповещения.Вставить("ДокументОснование", Объект.ДокументОснование);
	
	Оповестить("Справочник.ЗаменыЗадачПоПриоритизации.Изменение", ПараметрыДляОповещения, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ПричинаЗаменыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.ПричинаЗамены");
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСтарыеЗадачи

&НаКлиенте
Процедура СтарыеЗадачиЗадачаПриИзменении(Элемент)
	
	ОбновитьТрудозатратыВЗадачахНаСервере("СтарыеЗадачи", Элементы.СтарыеЗадачи.ТекущиеДанные.Задача);

КонецПроцедуры

&НаКлиенте
Процедура СтарыеЗадачиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.СтарыеЗадачи.ТекущиеДанные;
	ЗначениеПоля = "";
	
	Если Поле = Элементы.СтарыеЗадачиЗадача Тогда
		
		ЗначениеПоля = ТекущиеДанные.Задача;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеПоля) Тогда
		
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(, ЗначениеПоля);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНовыеЗадачи

&НаКлиенте
Процедура НовыеЗадачиЗадачаПриИзменении(Элемент)
	
	ОбновитьТрудозатратыВЗадачахНаСервере("НовыеЗадачи", Элементы.НовыеЗадачи.ТекущиеДанные.Задача);
	
КонецПроцедуры

&НаКлиенте
Процедура НовыеЗадачиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.НовыеЗадачи.ТекущиеДанные;
	ЗначениеПоля = "";
	
	Если Поле = Элементы.НовыеЗадачиЗадача Тогда
		
		ЗначениеПоля = ТекущиеДанные.Задача;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗначениеПоля) Тогда
		
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(, ЗначениеПоля);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьТрудозатратыПоЗадачам(Команда)
	
	ОбновитьТрудозатратыВЗадачахНаСервере("СтарыеЗадачи");
	ОбновитьТрудозатратыВЗадачахНаСервере("НовыеЗадачи");
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьНовуюЗадачуПоНомеруСсылке(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьЗадачуПоНомеруСсылкеПродолжение", ЭтотОбъект);
	Подсказка = НСтр("ru = 'Введите внешний номер задачи, идентификатор или ссылку.'");
	ПоказатьВводСтроки(ОписаниеОповещения, , Подсказка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЗадачуПоНомеруСсылкеПродолжение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		
		РезультатДобавления = ДобавитьЗадачуПоНомеруСсылкеНаСервере(Результат);
		ПоказатьПредупреждение(, РезультатДобавления.РезультатТекст);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ДобавитьЗадачуПоНомеруСсылкеНаСервере(ИдентификторЗадачи)
	
	РезультатДобавления = ПланированиеЗадачСервер.ДобавитьЗадачуПоНомеруСсылкеВТаблицу(Объект, "НовыеЗадачи", ИдентификторЗадачи);
	
	Если РезультатДобавления.ЗадачаДобавлена Тогда
		
		Элементы.НовыеЗадачи.ТекущаяСтрока = РезультатДобавления.ИдентификаторСтрокиЗадачи;

	КонецЕсли;
	
	Возврат РезультатДобавления;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьТрудозатратыВЗадачахНаСервере(ИмяТабличнойЧасти, Задача = Неопределено)
	
	ТаблицаДанных = Объект[ИмяТабличнойЧасти].Выгрузить();
	ПланированиеЗадачСервер.ОбновитьТрудозатратыВЗадачах(ТаблицаДанных, Задача);
	Объект[ИмяТабличнойЧасти].Загрузить(ТаблицаДанных);
	
КонецПроцедуры

#КонецОбласти
