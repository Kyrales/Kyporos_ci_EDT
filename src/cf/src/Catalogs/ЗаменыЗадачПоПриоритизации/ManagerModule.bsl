
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Сторока задач из списка для подстановки в наименование
//
// Параметры:
//  ИмяТабличнойЧасти - см. Справочники.ЗаменыЗадачПоПриоритизации.ТабличнаяЧасть.СтарыеЗадачи
//					  - см. Справочники.ЗаменыЗадачПоПриоритизации.ТабличнаяЧасть.НовыеЗадачи
//	НовыеЗадачи - Булевое - если Истина, то табличная часть НовыеЗадачи, а иначе СтарыеЗадачи
// 
// Возвращаемое значение:
//  Строка - строка задач из списка. Используемые реквизиты из задачи: Наименование, ВнешнийНомер, Номер.
//	Если ВнешнийНомер не заполнен, то выводится Номер.
//
// Пример: 
//	Если кол-во задач 1 в тч: "Модификация данных HR: CR-003231"
//	Если кол-во задач 2 в тч: "CR-003231|CR-003232"
//	Если кол-во задач 3 в тч: "CR-003231|CR-003232|CR-003233"
//	Если кол-во задач 0 в тч: "Без замены". Если НовыеЗадачи = Истина,то "Перенос на следующий период"
//
Функция СформироватьСтрокуЗадачСписка(ТекущаяТабличнаяЧасть, НовыеЗадачи = Ложь) Экспорт
	
	РезультатСтрокаЗадач = "";
	
	КоличествоЭлементовВТабличнойЧасти = ТекущаяТабличнаяЧасть.Количество();
	
	Если КоличествоЭлементовВТабличнойЧасти = 0 Тогда
		
		РезультатСтрокаЗадач = ?(НовыеЗадачи, НСтр("ru = 'Перенос на следующий период'"), НСтр("ru = 'Без замены'")); 
		
		Возврат РезультатСтрокаЗадач;
		
	КонецЕсли;
	
	ТолькоОднаЗадачаВСписке = КоличествоЭлементовВТабличнойЧасти = 1;
	
	ЗначенияРеквизитовЗадач = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ТекущаяТабличнаяЧасть.ВыгрузитьКолонку("Задача"), "Наименование, Номер, ВнешнийНомер");
	
	Для каждого ТекСтрокаТабличнойЧасти Из ТекущаяТабличнаяЧасть Цикл
	
		ДанныеРеквизитов = ЗначенияРеквизитовЗадач.Получить(ТекСтрокаТабличнойЧасти.Задача);
		
		// если внешний номер не заполнен, то берется текущий номер задачи
		НомерДляВставки = ?(ЗначениеЗаполнено(ДанныеРеквизитов.ВнешнийНомер), ДанныеРеквизитов.ВнешнийНомер, ДанныеРеквизитов.Номер);
	
		Если ТолькоОднаЗадачаВСписке
			И Не НовыеЗадачи Тогда
			
			НаименованиеДляВставки = Лев(ДанныеРеквизитов.Наименование, 70);
			
			РезультатСтрокаЗадач = СтрШаблон(НСтр("ru = '%1: %2'"), НомерДляВставки, НаименованиеДляВставки);			
			
			Прервать;
			
		Иначе
			
			ОбщегоНазначенияКупоросКлиентСервер.ПрисоединитьСтроку(РезультатСтрокаЗадач, НомерДляВставки, "|");
			
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат РезультатСтрокаЗадач;
	
КонецФункции // СформироватьСтрокуЗадачСписка()

#КонецОбласти

#КонецЕсли
