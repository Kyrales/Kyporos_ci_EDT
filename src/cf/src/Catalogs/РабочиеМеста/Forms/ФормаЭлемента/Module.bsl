
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов	
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства	
	
	СформироватьСписокРабочихИнструментов();	
	
	ОтображениеКартинки();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства	

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	Для каждого стр Из СписокАктивовДляРаботы Цикл
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ВидНоменклатуры", стр.ВидНоменклатуры);

		МассивНайденных = ТекущийОбъект.СписокРабочихИнструментов.НайтиСтроки(ПараметрыОтбора);
		Если МассивНайденных.Количество()>0 
			И (Не стр.Используется) Тогда
			 // удаляем неиспользуемый инструмент
			 ТекущийОбъект.СписокРабочихИнструментов.Удалить(МассивНайденных[0].НомерСтроки-1);
		 ИначеЕсли МассивНайденных.Количество()=0 
			 И стр.Используется Тогда
			 // добавляем используемый инструмент
			 НовСтр = ТекущийОбъект.СписокРабочихИнструментов.Добавить();
			 НовСтр.ВидНоменклатуры = стр.ВидНоменклатуры;
		КонецЕсли;
		
	КонецЦикла;
	
	ОбъектДанных = РеквизитФормыВЗначение("Объект");
	ТекущийОбъект.ИндексКартинки = Справочники.ВидыНоменклатуры.ПолучитьИндексКартинкиПоФайлуКартинки(ОбъектДанных.ФайлКартинки);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства	
	
	Если ИмяСобытия = "ИзмененУчетОборудования" Тогда
		СформироватьСписокРабочихИнструментов();
	КонецЕсли;
	
КонецПроцедуры

 &НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	УстановитьРеквизитКартинки(ВыбранноеЗначение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СерверПриИзменении(Элемент)
	
	СформироватьСписокРабочихИнструментов();	
	
	// СтандартныеПодсистемы.Свойства
	ОбновитьЭлементыДополнительныхРеквизитов();
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокАктивовДляРаботыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
		
	Если Элемент.ТекущиеДанные=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Для ввода/открытия оборудования объект справочника должен быть записан.'"));
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Элемент.ТекущиеДанные.Номенклатура) Тогда
		// открыть элемент оборудования
		КлючЗаписи = ПолучитьКлючЗаписиУчетаОборудования(Элемент.ТекущиеДанные.Номенклатура); 
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ключ", КлючЗаписи);

		ОткрытьФорму("РегистрСведений.УчетОборудованияРабочихМест.ФормаЗаписи", ПараметрыФормы, Элемент);	
		
	Иначе
		
		// создать новый элемент оборудования
		
		ЗначенияЗаполнения = Новый Структура;
		ЗначенияЗаполнения.Вставить("Организация", Объект.Организация);
 		ЗначенияЗаполнения.Вставить("РабочееМесто", Объект.Ссылка);
 		ЗначенияЗаполнения.Вставить("Количество", 1);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		ПараметрыФормы.Вставить("ИзФормыЭлементаРабочиеМеста", Истина);

		ОткрытьФорму("РегистрСведений.УчетОборудованияРабочихМест.ФормаЗаписи", ПараметрыФормы, Элемент);	

	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьСписок(Команда)
	
	СформироватьСписокРабочихИнструментов();	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИзНабора(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВладелецФайла",      Объект.Ссылка);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Истина);
	ПараметрыФормы.Вставить("АдресКартинки", АдресКартинки);
	
	ОткрытьФорму("ОбщаяФорма.ВыборИзображенияИзНабора", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьсяУдаленно(Команда)
	УдаленноеУправлениеКлиент.ПодключениеКРабочемуМесту(Объект.Ссылка, Объект.ОсновнойШаблонУдаленногоДоступа);	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ПОДСИСТЕМЫ СВОЙСТВ

&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
    УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
    УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ПОДСИСТЕМЫ СВОЙСТВ

&НаСервере
Процедура СформироватьСписокРабочихИнструментов()
	
	// АПК:494-выкл Допускаем соеденять таблицу с вложенным запросом 
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВидыНоменклатуры.Ссылка КАК ВидНоменклатуры,
	               |	ЕСТЬNULL(ВложенныйЗапрос.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК Номенклатура,
	               |	ВложенныйЗапрос.Количество,
	               |	ВидыНоменклатуры.ИндексКартинки КАК ИндексКартинкиВидНоменклатуры,
	               |	ВЫБОР
	               |		КОГДА ВложенныйЗапрос.Номенклатура ЕСТЬ NULL 
	               |			ТОГДА 2
	               |		ИНАЧЕ 1
	               |	КОНЕЦ КАК Приоритет
	               |ИЗ
	               |	Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			УчетОборудованияРабочихМест.Организация КАК Организация,
	               |			УчетОборудованияРабочихМест.Номенклатура КАК Номенклатура,
	               |			УчетОборудованияРабочихМест.РабочееМесто КАК РабочееМесто,
	               |			УчетОборудованияРабочихМест.Количество КАК Количество
	               |		ИЗ
	               |			РегистрСведений.УчетОборудованияРабочихМест КАК УчетОборудованияРабочихМест
	               |		ГДЕ
	               |			УчетОборудованияРабочихМест.РабочееМесто = &РабочееМесто) КАК ВложенныйЗапрос
	               |		ПО ВидыНоменклатуры.Ссылка = ВложенныйЗапрос.Номенклатура.ВидНоменклатуры
	               |ГДЕ
	               |	НЕ ВидыНоменклатуры.ЭтоГруппа
	               |	И НЕ ВидыНоменклатуры.ПометкаУдаления
	               |	И ВЫБОР
	               |			КОГДА &ЕстьСервер ИЛИ (НЕ ВложенныйЗапрос.Номенклатура ЕСТЬ NULL)
	               |				ТОГДА ИСТИНА
	               |			ИНАЧЕ НЕ ВидыНоменклатуры.Сервер
	               |		КОНЕЦ
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Приоритет,
	               |	ВидыНоменклатуры.Наименование";
	
	Запрос.УстановитьПараметр("РабочееМесто",Объект.Ссылка);
	Запрос.УстановитьПараметр("ЕстьСервер",Объект.Сервер);
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выгрузить();
	
	СписокАктивовДляРаботы.Загрузить(Выборка);	
	
	Для каждого стр Из СписокАктивовДляРаботы Цикл
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ВидНоменклатуры", стр.ВидНоменклатуры);

		МассивНайденных = Объект.СписокРабочихИнструментов.НайтиСтроки(ПараметрыОтбора);
		Если МассивНайденных.Количество()>0 Тогда
			 стр.Используется = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьКлючЗаписиУчетаОборудования(Номенклатура)
	
	СтруктураКлюча = Новый Структура;
	СтруктураКлюча.Вставить("Организация",  Объект.Организация);		
	СтруктураКлюча.Вставить("РабочееМесто", Объект.Ссылка);
	СтруктураКлюча.Вставить("Номенклатура", Номенклатура);

	Возврат РегистрыСведений.УчетОборудованияРабочихМест.СоздатьКлючЗаписи(СтруктураКлюча);

КонецФункции // ПолучитьКлючЗаписиДокФизЛиц()

&НаСервереБезКонтекста
Функция ПолучитьНавигационнуюСсылкуКартинки(ФайлКартинки, ИдентификаторФормы)

	УстановитьПривилегированныйРежим(Истина);
	
	ДвоичныеДанные = ФайлКартинки.Получить();
	Если ТипЗнч(ИдентификаторФормы) = Тип("УникальныйИдентификатор") Тогда
		СсылкаНаДвоичныеДанныеФайла = ПоместитьВоВременноеХранилище(ДвоичныеДанные, ИдентификаторФормы);
	Иначе
		СсылкаНаДвоичныеДанныеФайла = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	КонецЕсли;
	
	ДополнительнаяИнформация = Новый Структура;
	ДополнительнаяИнформация.Вставить("СсылкаНаДвоичныеДанныеФайла",  СсылкаНаДвоичныеДанныеФайла);
	
	Возврат ДополнительнаяИнформация.СсылкаНаДвоичныеДанныеФайла;
	
КонецФункции

&НаСервере
Процедура ОтображениеКартинки()
	
	// Установка значения реквизита АдресКартинки.
	Если Не Объект.Ссылка.Пустая() Тогда
		ОбъектДанных = РеквизитФормыВЗначение("Объект");
		Если ОбъектДанных.ФайлКартинки.Получить()<>Неопределено Тогда
			АдресКартинки = ПолучитьНавигационнуюСсылкуКартинки(ОбъектДанных.ФайлКартинки, УникальныйИдентификатор)
		Иначе
			АдресКартинки = "";
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьРеквизитКартинки(ВыбраннаяКартинка) 
	
	ОбъектДанных = РеквизитФормыВЗначение("Объект");
	ОбъектДанных.ФайлКартинки = Новый ХранилищеЗначения(ВыбраннаяКартинка);
	ОбъектДанных.Записать();
	
	ЗначениеВРеквизитФормы(ОбъектДанных,"Объект");	
	
	АдресКартинки = ПолучитьНавигационнуюСсылкуКартинки(ОбъектДанных.ФайлКартинки, УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти


