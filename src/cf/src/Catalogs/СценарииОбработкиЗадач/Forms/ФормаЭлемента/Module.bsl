

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов	
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаполнениеПереходаПоСостояниюЗавершение" Тогда
	
		ЗаполнениеПереходаПоСостояниюЗавершение(Параметр);
	
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЭтапыСостоянийПриИзменении(Элемент)
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Элемент.ТекущиеДанные.Состояние) Тогда
		Возврат;
	КонецЕсли;
	
	Элемент.ТекущиеДанные.ПереходыНаСостояния = ТекстПереходаНаСостояния(Элемент.ТекущиеДанные.Состояние,Объект.ПереходыПоСостояниям);
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыСостоянийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ЭтапыСостоянийПереходыНаСостояния" Тогда
		
		ВыбранноеСостояние = Элемент.ТекущиеДанные.Состояние;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Заголовок",""+ВыбранноеСостояние);
		ПараметрыФормы.Вставить("Состояние",ВыбранноеСостояние);
		ПараметрыФормы.Вставить("АдресПереходыПоСостояниям",АдресПереходаСостояния(ВыбранноеСостояние));
		
		ОткрытьФорму("Справочник.СценарииОбработкиЗадач.Форма.ЗаполнениеПереходаПоСостоянию",ПараметрыФормы,ЭтаФорма,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьСценариемОбработкиПоУмолчанию(Команда)
	
	Если Объект.Ссылка.Пустая() Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Необходимо записать сценарий.'"));
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьСценариемОбработкиПоУмолчаниюЗавершение", ЭтотОбъект);
	
	ПоказатьВопрос(ОписаниеОповещения, НСтр("ru = 'Полностью перезаполнить сценарием по-умолчанию текущий?'"),РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСценариемОбработкиПоУмолчаниюЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСценариемОбработкиПоУмолчаниюНаСервере(Объект.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнениеПереходаПоСостояниюЗавершение(Результат)
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
		
	// Очистить старый вариант по состоянию.
	КолСтрокИсходное = Объект.ПереходыПоСостояниям.Количество();
	индекс = 1; сч = 0;
	Пока индекс <= КолСтрокИсходное Цикл
		Если Результат.Состояние = Объект.ПереходыПоСостояниям[сч].Состояние Тогда
			Объект.ПереходыПоСостояниям.Удалить(Объект.ПереходыПоСостояниям[сч]);    
		Иначе
			сч = сч + 1;    
		КонецЕсли;
		индекс = индекс + 1;
	КонецЦикла;
	
	// Заполнение нового варианта переходов.	
	Для каждого ТекСтрокаПерехода Из Результат.ПереходыПоСостояниям Цикл
	
		НовСтрокаПерехода = Объект.ПереходыПоСостояниям.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтрокаПерехода,ТекСтрокаПерехода);
		НовСтрокаПерехода.Состояние = Результат.Состояние;
		
	КонецЦикла;
	
	Модифицированность = Истина;
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура УправлениеФормой()

	// Заполнить ПереходыНаСостояния.	
	Для каждого ТекСостояние Из ЭтотОбъект.Объект.ЭтапыСостояний Цикл
	
		ТекСостояние.ПереходыНаСостояния = ТекстПереходаНаСостояния(ТекСостояние.Состояние,Объект.ПереходыПоСостояниям);
		
	КонецЦикла;
	
КонецПроцедуры // УправлениеФормой()

&НаКлиентеНаСервереБезКонтекста
Функция ТекстПереходаНаСостояния(Состояние,ПереходыПоСостояниям)
	
	ПараметрыОтбора = Новый Структура("Состояние",Состояние);
	СостоянияПерехода = ПереходыПоСостояниям.НайтиСтроки(ПараметрыОтбора);
	
	Если СостоянияПерехода.Количество()=0 Тогда
		Возврат НСтр("ru = 'Нет переходов.'");
	КонецЕсли;
	
	ТекстПерехода = "";
	Для каждого ТекСтрокаПерехода Из СостоянияПерехода Цикл
	
		ТекстПерехода = ТекстПерехода + СтрШаблон(НСтр("ru = '%1; '"),ТекСтрокаПерехода.СостояниеПерехода);
	
	КонецЦикла;
	
	Возврат ТекстПерехода;
	
КонецФункции // ТекстПереходаНаСостояния()

&НаСервере
Функция АдресПереходаСостояния(Состояние)
	
	ПараметрыОтбора = Новый Структура("Состояние",Состояние);
	СостоянияПерехода = Объект.ПереходыПоСостояниям.НайтиСтроки(ПараметрыОтбора);
	
	ТаблицаСостоянийПерехода = Новый ТаблицаЗначений;
	ТаблицаСостоянийПерехода.Колонки.Добавить("СостояниеПерехода");
	ТаблицаСостоянийПерехода.Колонки.Добавить("ОсновнойПереход");
	
	Для каждого ТекСтрока Из СостоянияПерехода Цикл
		
		НовСтрокаТаблицы = ТаблицаСостоянийПерехода.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтрокаТаблицы,ТекСтрока);
	
	КонецЦикла;
	
	Адрес = ПоместитьВоВременноеХранилище(ТаблицаСостоянийПерехода,УникальныйИдентификатор);
	
	Возврат Адрес;

КонецФункции // АдресПереходаСостояния()

&НаСервере
Процедура ЗаполнитьСценариемОбработкиПоУмолчаниюНаСервере(СценарийОбработки)
	
	ДанныеСценарияПоУмолчанию = Справочники.СценарииОбработкиЗадач.СтруктураДанныхСценарияПоУмолчанию();
	
	Объект.ЭтапыСостояний.Загрузить(ДанныеСценарияПоУмолчанию.ЭтапыСостояний);
	Объект.ПереходыПоСостояниям.Загрузить(ДанныеСценарияПоУмолчанию.ПереходыПоСостояниям);
	
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти





