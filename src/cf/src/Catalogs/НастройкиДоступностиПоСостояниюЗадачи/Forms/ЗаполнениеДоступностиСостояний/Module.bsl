
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Заголовок = Параметры.Заголовок;
	ИмяПоляКоманды = Параметры.ИмяПоляКоманды;
	
	ТаблицаИзДругойФормыПоРедактированиюСостояний = ПолучитьИзВременногоХранилища(Параметры.АдресРедактированияСостоянийКоманды);
	НастройкаДоступности.Загрузить(ТаблицаИзДругойФормыПоРедактированиюСостояний);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ПараметрыЗакрытия = Новый Структура;
	ПараметрыЗакрытия.Вставить("НастройкаДоступности", НастройкаДоступности);
	ПараметрыЗакрытия.Вставить("ИмяПоляКоманды", ИмяПоляКоманды);
	
	Оповестить("ЗаполнениеДоступностиСостоянияПоляКоманды", ПараметрыЗакрытия);
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСостояния(Команда)
	ОбновитьСостоянияНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьСостоянияНаСервере()
	
	МассивСостояний = НастройкаДоступности.Выгрузить(, "Состояние").ВыгрузитьКолонку("Состояние"); 
	ТаблицаАктуальныхСостояний = Справочники.СостоянияЗадач.ВсеАктуальныеСостояния(МассивСостояний, Истина);
	
	Для каждого ТекСтрока Из ТаблицаАктуальныхСостояний Цикл
	
		НоваяСтрокаДанных = НастройкаДоступности.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаДанных, ТекСтрока);
		
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти

