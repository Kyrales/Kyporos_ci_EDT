
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	 ЗаполнитьНастройкиДоступности();
	 
	 УправлениеФормой();
	 
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	 УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаполнениеДоступностиСостоянияПоляКоманды" Тогда
	
		ЗаполнениеДоступностиСостоянияПоляКомандыЗавершение(Параметр);
	
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ЗаполнитьСтрокиТаблЧастиНастройкиДоступности();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	 УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НастройкиДоступностиУправлятьЭлементомНаФормеЗадачиПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНастройкиДоступности

&НаКлиенте
Процедура НастройкиДоступностиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "НастройкиДоступностиСостояние" Тогда
		
		ЗаполнитьСтрокиТаблЧастиНастройкиДоступности();
		
		ПредставлениеПоляКоманды = Элемент.ТекущиеДанные.ПредставлениеПоляКоманды;
		ИмяПоляКоманды = Элемент.ТекущиеДанные.ИмяПоля;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Заголовок", ПредставлениеПоляКоманды);
		ПараметрыФормы.Вставить("ИмяПоляКоманды", ИмяПоляКоманды);
		ПараметрыФормы.Вставить("АдресРедактированияСостоянийКоманды", АдресРедактированияСостоянийКоманды(ИмяПоляКоманды));
		
		ОткрытьФорму("Справочник.НастройкиДоступностиПоСостояниюЗадачи.Форма.ЗаполнениеДоступностиСостояний", ПараметрыФормы, ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьБазовымиНастройкамиСостояний(Команда)
	
	Если Объект.Ссылка.Пустая() Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Необходимо записать настройку доступности.'"));
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьБазовымиНастройкамиСостоянийЗавершение", ЭтотОбъект);
	
	ПоказатьВопрос(ОписаниеОповещения, НСтр("ru = 'Перезаписать на базовые настройки состояний текущий объект?'"),РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьБазовымиНастройкамиСостоянийЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьБазовымиНастройкамиСостоянийНаСервере(Объект.Ссылка, Объект.ТипПользователяЗадач);
	
	ЭтаФорма.Прочитать();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьНастройкиДоступности()

	ТаблНастройкиДоступности = РеквизитФормыВЗначение("НастройкиДоступностиСвернутые");
	ТаблНастройкиДоступности.Очистить();
	
	ТаблицаИменПолейИКоманд = Справочники.НастройкиДоступностиПоСостояниюЗадачи.ИменаПолейИКомандДляНастройкиДоступности();
	
	Для каждого ТекСтрокаИменПолей Из ТаблицаИменПолейИКоманд Цикл
	
		НоваяСтрока = ТаблНастройкиДоступности.Добавить();
		НоваяСтрока.ИмяПоля = ТекСтрокаИменПолей.ИмяПоляКоманды;
		НоваяСтрока.ПредставлениеПоляКоманды = ТекСтрокаИменПолей.ПредставлениеПоляКоманды;
	
	КонецЦикла;
	
	ТаблНастройкиДоступности.Сортировать("ИмяПоля");
	
	ЗначениеВРеквизитФормы(ТаблНастройкиДоступности, "НастройкиДоступностиСвернутые");
	
КонецПроцедуры // ЗаполнитьНастройкиДоступности()

&НаКлиенте
Процедура ЗаполнениеДоступностиСостоянияПоляКомандыЗавершение(Результат)
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
		
	// Очистить старый вариант по имени поля команды.
	КолСтрокИсходное = Объект.НастройкиДоступности.Количество();
	сч = 1; индекс = 0;
	Пока сч <= КолСтрокИсходное Цикл
		Если Результат.ИмяПоляКоманды = Объект.НастройкиДоступности[индекс].ИмяПоляКоманды Тогда
			Объект.НастройкиДоступности.Удалить(Объект.НастройкиДоступности[индекс]);    
		Иначе
			индекс = индекс + 1;    
		КонецЕсли;
		сч = сч + 1;
	КонецЦикла;
	
	// Заполнение нового варианта переходов.	
	Для каждого ТекСтрока Из Результат.НастройкаДоступности Цикл
	
		НовСтрока = Объект.НастройкиДоступности.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтрока, ТекСтрока);
		НовСтрока.ИмяПоляКоманды = Результат.ИмяПоляКоманды;
		
		Если Не ЗначениеЗаполнено(НовСтрока.ВариантОграниченияНаФормеЗадачи) Тогда
			НовСтрока.ВариантОграниченияНаФормеЗадачи = ПредопределенноеЗначение("Перечисление.ВариантыОграниченияДоступаНаФорме.Видимость");
		КонецЕсли;
		
	КонецЦикла;
	
	Модифицированность = Истина;
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСтрокиТаблЧастиНастройкиДоступности()

	Для каждого ТекСтрока Из НастройкиДоступностиСвернутые Цикл
	
		ПараметрыОтбора = Новый Структура("ИмяПоляКоманды", ТекСтрока.ИмяПоля);
		НайденныеНастройкиДоступностиОбъекта = Объект.НастройкиДоступности.НайтиСтроки(ПараметрыОтбора);
		
		Для каждого ТекДанныеОбъекта Из НайденныеНастройкиДоступностиОбъекта Цикл
			
			ТекДанныеОбъекта.ВариантОграниченияНаФормеЗадачи = ТекСтрока.ВариантОграниченияНаФормеЗадачи;
			
		КонецЦикла;
	
	КонецЦикла;	
	
КонецПроцедуры // ЗаполнитьСтрокиТаблЧастиНастройкиДоступности()

&НаСервере
Процедура УправлениеФормой()

	Для каждого ТекСостояние Из НастройкиДоступностиСвернутые Цикл
	
		ДанныеСтрокиНастройки = ДанныеСтрокиНастройкиДоступности(ТекСостояние.ИмяПоля, Объект.НастройкиДоступности);
		
		ЗаполнитьЗначенияСвойств(ТекСостояние, ДанныеСтрокиНастройки);
		
	КонецЦикла;
	
КонецПроцедуры // УправлениеФормой()

&НаСервере
Функция АдресРедактированияСостоянийКоманды(ИмяПоляКоманды)
	
	ПараметрыОтбора = Новый Структура("ИмяПоляКоманды", ИмяПоляКоманды);
	НастройкиСостояния = Объект.НастройкиДоступности.НайтиСтроки(ПараметрыОтбора);
	
	ТаблицаСостояний = Новый ТаблицаЗначений;
	ТаблицаСостояний.Колонки.Добавить("Состояние");
	ТаблицаСостояний.Колонки.Добавить("Доступность");
	ТаблицаСостояний.Колонки.Добавить("ПредставлениеПоляКоманды");
	ТаблицаСостояний.Колонки.Добавить("ВариантОграниченияНаФормеЗадачи");
	
	Для каждого ТекСтрока Из НастройкиСостояния Цикл
		
		НовСтрокаТаблицы = ТаблицаСостояний.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтрокаТаблицы, ТекСтрока);
	
	КонецЦикла;
	
	Адрес = ПоместитьВоВременноеХранилище(ТаблицаСостояний, УникальныйИдентификатор);
	
	Возврат Адрес;

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ДанныеСтрокиНастройкиДоступности(ИмяПоляКоманды, НастройкиДоступности)
	
	ДанныеСтрокиНастройки = Новый Структура;
	ДанныеСтрокиНастройки.Вставить("Состояние","");
	ДанныеСтрокиНастройки.Вставить("ВариантОграниченияНаФормеЗадачи",ПредопределенноеЗначение("Перечисление.ВариантыОграниченияДоступаНаФорме.Видимость"));
		
	ПараметрыОтбора = Новый Структура("ИмяПоляКоманды", ИмяПоляКоманды);
	НастройкиСостояния = НастройкиДоступности.НайтиСтроки(ПараметрыОтбора);
	
	ТекстСостояния = "";
	
	Если НастройкиСостояния.Количество() = 0 Тогда
		ДанныеСтрокиНастройки.Состояние = НСтр("ru = 'Нет состояний.'");
		Возврат ДанныеСтрокиНастройки;
	КонецЕсли;
	
	Для каждого ТекСтрока Из НастройкиСостояния Цикл
		
		Если ТекСтрока.Доступность = ПредопределенноеЗначение("Перечисление.ДоступностьПолей.ПустаяСсылка") 
			Или ТекСтрока.Доступность = ПредопределенноеЗначение("Перечисление.ДоступностьПолей.НеНазначено") Тогда
			Продолжить;
		КонецЕсли;
		
		// состояние
		ТекстДоступность = ?(ТекСтрока.Доступность = ПредопределенноеЗначение("Перечисление.ДоступностьПолей.Доступно"), НСтр("ru = 'Да'"), НСтр("ru = 'Нет'")); 
		ОбщегоНазначенияКупоросКлиентСервер.ПрисоединитьСтроку(ДанныеСтрокиНастройки.Состояние, СтрШаблон(НСтр("ru = '[%1: %2]'"), ТекСтрока.Состояние,  ТекстДоступность), " ");
		
		// вариант управления элементом на форме задачи
		ДанныеСтрокиНастройки.ВариантОграниченияНаФормеЗадачи = ТекСтрока.ВариантОграниченияНаФормеЗадачи;
		
	КонецЦикла;
	
	Если ДанныеСтрокиНастройки.Состояние = "" Тогда
		ДанныеСтрокиНастройки.Состояние = НСтр("ru = 'Нет состояний.'");	
	КонецЕсли;
		
	Возврат ДанныеСтрокиНастройки;
	
КонецФункции 

&НаСервере
Процедура ЗаполнитьБазовымиНастройкамиСостоянийНаСервере(СсылкаНастройкиДоступности, СсылкаТипПользователяЗадач)
	
	ОбъектДляИзменения = СсылкаНастройкиДоступности.ПолучитьОбъект();
	
	ОбъектДляИзменения.Заблокировать();
	
	Справочники.НастройкиДоступностиПоСостояниюЗадачи.СценарийПоУмолчаниюДляТипаПользователяЗадач(ОбъектДляИзменения, СсылкаТипПользователяЗадач);
	
	ОбъектДляИзменения.Записать();
	
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти




