

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбновитьТаблицуСервисы();
	
	// #РаскраскаКода
	РаскраситьПрограммныйКодHTML("КодСборкиОтвета", "КодСборкиОтветаHTML");

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	// #РаскраскаКода
	Объект.КодСборкиОтвета = ОбщегоНазначенияКупоросКлиент.КодПрограммыИзРаскрашенногоHTML(Элементы.КодСборкиОтветаHTML);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОбновитьТаблицуСервисы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сообщение(Команда)
	
	ОткрытьФормуСозданияШаблона(НСтр("ru = 'Создание шаблона отправки сообщения'"), "sendMessage", "ОтправитьСообщение");
	
КонецПроцедуры

&НаКлиенте
Процедура Фото(Команда)
	
	ОткрытьФормуСозданияШаблона(НСтр("ru = 'Создание шаблона отправки фотографии'"), "sendPhoto", "ОтправитьФотографию");
	
КонецПроцедуры

&НаКлиенте
Процедура Видео(Команда)
	
	ОткрытьФормуСозданияШаблона(НСтр("ru = 'Создание шаблона отправки видео'"), "sendVideo", "ОтправитьВидео");
	
КонецПроцедуры

&НаКлиенте
Процедура Файл(Команда)
	
	ОткрытьФормуСозданияШаблона(НСтр("ru = 'Создание шаблона отправки файла'"), "sendDocument", "ОтправитьДокумент");
	
КонецПроцедуры

&НаКлиенте
Процедура Аудио(Команда)
	
	ОткрытьФормуСозданияШаблона(НСтр("ru = 'Создание шаблона отправки аудио'"), "sendAudio", "ОтправитьАудио");
	
КонецПроцедуры

&НаКлиенте
Процедура Голосовое(Команда)
	
	ОткрытьФормуСозданияШаблона(НСтр("ru = 'Создание шаблона отправки голосовой записи'"), "sendVoice", "ОтправитьГолос");
	
КонецПроцедуры

&НаКлиенте
Процедура Местоположение(Команда)
	
	ОткрытьФормуСозданияШаблона(НСтр("ru = 'Создание шаблона отправки местоположения'"), "sendLocation", "ОтправитьМестоположение");
	
КонецПроцедуры

&НаКлиенте
Процедура МестоНаКарте(Команда)
	
	ОткрытьФормуСозданияШаблона(НСтр("ru = 'Создание шаблона отправки места на карте'"), "sendVenue", "ОтправитьМестоНаКарте");
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьКонтекстнуюКлавиатуру(Команда)
	
	ОткрытьФормуСозданияШаблона(НСтр("ru = 'Редактирование контекстной клавиатуры'"), "editMessageReplyMarkup", "РедактироватьКонтекстнуюКлавиатуру");	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьФайл(Команда)
	
	// #РаскраскаКода
	Объект.КодСборкиОтвета = ОбщегоНазначенияКупоросКлиент.КодПрограммыИзРаскрашенногоHTML(Элементы.КодСборкиОтветаHTML);
	
	ТекстМакета = НСтр("ru = '// Загружается двоичное содержимое сообщения, что бы это ни было - документ, картинка, аудио, видео, голос.
                        |// Файл сохраняется в папке, указанной в реквизите """"Каталог сохранения файлов"""" бота.
                        |// Результат загрузки попадает в НакопленныеДанные.ЗагруженныйФайл (<строка> Или НЕОПРЕДЕЛЕНО) 
                        |ЗагрузитьФайл(НакопленныеДанные);'");
	
	Если ЗначениеЗаполнено(Объект.КодСборкиОтвета) Тогда
		РазделительКода = Символы.ПС + Символы.ПС + Символы.ПС;
	Иначе
		РазделительКода = "";
	КонецЕсли;
	Объект.КодСборкиОтвета = Объект.КодСборкиОтвета + РазделительКода + ТекстМакета;
	
	// #РаскраскаКода
	РаскраситьПрограммныйКодHTML("КодСборкиОтвета", "КодСборкиОтветаHTML");
	
	ЭтаФорма.Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьСообщение(Команда)
	
	ОткрытьФормуСозданияШаблона(НСтр("ru = 'Редактирование отправленного ранее сообщения'"), "editMessageText", "ИзменитьТекстСообщения");
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСообщение(Команда)
	
	ОткрытьФормуСозданияШаблона(НСтр("ru = 'Удаление отправленного ранее сообщения'"), "deleteMessage", "УдалитьСообщение");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОткрытьФормуСозданияШаблона(ЗаголовокФормы, Метод, НазваниеПроцедуры)
	
	// #РаскраскаКода
	Объект.КодСборкиОтвета = ОбщегоНазначенияКупоросКлиент.КодПрограммыИзРаскрашенногоHTML(Элементы.КодСборкиОтветаHTML);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Заголовок", 		ЗаголовокФормы);
	ПараметрыОткрытия.Вставить("Метод", 			Метод);
	ПараметрыОткрытия.Вставить("НазваниеПроцедуры", НазваниеПроцедуры);
	ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьКодВыбор", ЭтаФорма);
	ОткрытьФорму("Справочник.ТелеграмОбработка.Форма.ФормаСозданияШаблона", ПараметрыОткрытия, ЭтаФорма,,,, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКодВыбор(ТекстМакета, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(ТекстМакета) = Тип("Строка") Тогда
		
		Если ЗначениеЗаполнено(Объект.КодСборкиОтвета) Тогда
			РазделительКода = Символы.ПС + Символы.ПС + Символы.ПС;
		Иначе
			РазделительКода = "";
		КонецЕсли;
		Объект.КодСборкиОтвета = Объект.КодСборкиОтвета + РазделительКода + ТекстМакета;
		
		// #РаскраскаКода
		РаскраситьПрограммныйКодHTML("КодСборкиОтвета", "КодСборкиОтветаHTML");
		
		ЭтаФорма.Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьТаблицуСервисы()
	
	Сервисы.Очистить();
	
	Если Объект.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Обработка", Объект.Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТелеграмСервисОбработки.Ссылка КАК Сервис
	|ИЗ
	|	Справочник.ТелеграмСервис.Обработки КАК ТелеграмСервисОбработки
	|ГДЕ
	|	ТелеграмСервисОбработки.Обработка = &Обработка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТелеграмСервисОбработки.Ссылка.Код";
	
	Сервисы.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

// #РаскраскаКода
&НаСервере
Процедура РаскраситьПрограммныйКодHTML(РеквизитОбъекта, РаскрашиваемыйРеквизитФормыHTML)

	ОбъектВыполняторКода = Обработки.ВыполняторКода.Создать();
	ЭтаФорма[РаскрашиваемыйРеквизитФормыHTML] = ОбъектВыполняторКода.РаскрашеннаяСтраница1C(Объект[РеквизитОбъекта]);

КонецПроцедуры

#КонецОбласти

