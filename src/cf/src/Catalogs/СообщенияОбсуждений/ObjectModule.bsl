#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Устанавливает реквизит СокращенТекстСписка и ТекстСообщенияСписок.
//
Процедура УстановитьТекстСписка() Экспорт
	
	МаксимальнаяДлинаСтрокиСписок = 105;
	МаксимальнаяДлинаТекстаСписок = 1024;
	МаксимальноеЧислоСтрок = 6;
	
	// Длинный текст
	ДлинаТекста = СтрДлина(ТекстСообщения);
	Если ДлинаТекста > МаксимальнаяДлинаТекстаСписок Тогда
		ТекстСообщенияСписок = Лев(ТекстСообщения, МаксимальнаяДлинаТекстаСписок - 3) + "...";
		СокращенТекстСписка = Истина;
		Возврат;
	КонецЕсли;
	
	ТекстСообщенияСписок = ТекстСообщения;
	
	// Большое количество строк
	ЧислоСтрок = СтрЧислоСтрок(ТекстСообщения);
	Если ЧислоСтрок > МаксимальноеЧислоСтрок Тогда
		СокращенТекстСписка = Истина;
		Возврат;
	КонецЕсли;
	
	// Большое количество длинных строк
	ЧислоОтображаемыхСтрок = 0;
	Для НомерСтроки = 1 По ЧислоСтрок Цикл
		ОчереднаяСтрока = СтрПолучитьСтроку(ТекстСообщения, НомерСтроки);
		ДлинаОчереднойСтроки = СтрДлина(ОчереднаяСтрока);
		ЧислоОтображаемыхСтрок = 
			ЧислоОтображаемыхСтрок + 1 + Цел(ДлинаОчереднойСтроки/МаксимальнаяДлинаСтрокиСписок);
	КонецЦикла;
	Если ЧислоОтображаемыхСтрок > МаксимальноеЧислоСтрок Тогда
		СокращенТекстСписка = Истина;
		Возврат;
	КонецЕсли;
	
	СокращенТекстСписка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Автор = Пользователи.ТекущийПользователь();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ТемыОбсуждений") Тогда
		ТемаЗаполнения = ДанныеЗаполнения;
		ПервоеСообщениеТемыЗаполнения = РаботаСОбсуждениями.НайтиПервоеСообщениеТемы(ТемаЗаполнения);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПервоеСообщениеТемыЗаполнения,
			"ТекстСообщения, ВопросГолосования, ДобавленоГолосование");
		ВариантыГолосования.Загрузить(ПервоеСообщениеТемыЗаполнения.ВариантыГолосования.Выгрузить());
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Не ПервоеСообщениеТемы Тогда
		ТемаЗакрыта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВладелецСообщения, "Закрытая");
		Если ТемаЗакрыта Тогда
			Отказ = Истина;
			Текст = НСтр("ru = 'Невозможно произвести запись сообщения в закрытую тему.'");
			ОбщегоНазначения.СообщитьПользователю(Текст, , "Объект.ТекстСообщения");
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ДобавленоГолосование Тогда
		
		Если ВариантыГолосования.Количество() = 1 Тогда
			ОбщегоНазначения.СообщитьПользователю(
				НСтр("ru = 'Нужно указать хотя бы два варианта ответа голосования'"),,
					"ВариантыГолосования",
					"Объект",
					Отказ);
		КонецЕсли;
		
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("ВопросГолосования");
		МассивНепроверяемыхРеквизитов.Добавить("ВариантыГолосования");
		МассивНепроверяемыхРеквизитов.Добавить("ВариантыГолосования.ТекстВарианта");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПредыдущаяПометкаУдаления = Ложь;
	Если Не Ссылка.Пустая() Тогда
		ПредыдущаяПометкаУдаления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ПометкаУдаления");
	КонецЕсли;
	
	ОбновленоСообщение = Ложь;
	
	РабочаяГруппаДобавить = Неопределено;
	РабочаяГруппаУдалить = Неопределено;
	Если ДополнительныеСвойства.Свойство("РабочаяГруппаДобавить") Тогда
		РабочаяГруппаДобавить = ДополнительныеСвойства.РабочаяГруппаДобавить;
	КонецЕсли;
	Если ДополнительныеСвойства.Свойство("РабочаяГруппаУдалить") Тогда
		РабочаяГруппаУдалить = ДополнительныеСвойства.РабочаяГруппаУдалить;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		
		Если ДополнительныеСвойства.Свойство("СозданиеНовойТемы")
				И ДополнительныеСвойства.Свойство("РеквизитыТемы")
				И ДополнительныеСвойства.СозданиеНовойТемы Тогда
			
			РеквизитыТемы = ДополнительныеСвойства.РеквизитыТемы;
			ПредметТемы = РеквизитыТемы.Документ;
			
			ЕстьТемаПоПредмету = Ложь;
			Если ЗначениеЗаполнено(ПредметТемы) Тогда
				ТемаПоПредмету = РаботаСОбсуждениями.НайтиТемуПоПредмету(ПредметТемы);
				ЕстьТемаПоПредмету = ЗначениеЗаполнено(ТемаПоПредмету);
			КонецЕсли;
			
			Если ЕстьТемаПоПредмету Тогда
				ВладелецСообщения = ТемаПоПредмету;
				ПервоеСообщениеТемы = Ложь;
			Иначе
				ВладелецСообщения = Справочники.ТемыОбсуждений.СоздатьНовуюТему(
					РеквизитыТемы, Автор, РабочаяГруппаДобавить, РабочаяГруппаУдалить);
			КонецЕсли;
			
		КонецЕсли;
		
		ДополнительныеСвойства.Вставить("ОбновленоСообщение", Истина);
		ДополнительныеСвойства.Вставить("ЭтоНовый", Истина);
		
		ДатаСоздания = ТекущаяДатаСеанса();
		НомерСообщения = РаботаСОбсуждениями.ПосчитатьНомерНовогоСообщения(ВладелецСообщения);
		Наименование = РаботаСОбсуждениями.ПолучитьЗаголовок(ТекстСообщения);
		УстановитьТекстСписка();
		
		Если ПервоеСообщениеТемы  Тогда
			Если ЕстьПервоеСообщенияТемы(ВладелецСообщения) Тогда
				ПервоеСообщениеТемы = Ложь;
			ИначеЕсли Не (ДополнительныеСвойства.Свойство("НеМенятьАвтораТемы") И ДополнительныеСвойства.НеМенятьАвтораТемы)  Тогда 
				ИзменитьАвтораТемы(ВладелецСообщения);
			КонецЕсли;
		Иначе
			Если Не ЕстьПервоеСообщенияТемы(ВладелецСообщения) Тогда
				Если Не (ДополнительныеСвойства.Свойство("НеМенятьАвтораТемы") И ДополнительныеСвойства.НеМенятьАвтораТемы) Тогда
					ИзменитьАвтораТемы(ВладелецСообщения);
				КонецЕсли;
				ПервоеСообщениеТемы = Истина;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ПометкаУдаления <> ПредыдущаяПометкаУдаления Тогда
		
		Если Не (ПривилегированныйРежим() Или Пользователи.ЭтоПолноправныйПользователь()) Тогда 
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'У вас нет права ""Пометка на удаление"" сообщения обсуждения ""%1"".'"),
				Строка(Ссылка));
		КонецЕсли;
		
		ДополнительныеСвойства.Вставить("ОбновленоСообщение", Ложь);
		Попытка
			РаботаСФайлами.УстановитьПометкуУдаленияПрисоединенныхФайлов(ЭтотОбъект,Отказ);
		Исключение
			Инфо = ИнформацияОбОшибке();
			ВызватьИсключение (Инфо.Описание);
		КонецПопытки;
		
		Если ПервоеСообщениеТемы Тогда
			Если ПометкаУдаления <> ВладелецСообщения.ПометкаУдаления Тогда
				// Попытка заблокировать первое сообщение, если удаляем из списка.
				УстановитьПривилегированныйРежим(Истина);
				Попытка
					ЭтотОбъект.Заблокировать();
				Исключение
					// Если уже заблокировано, значит работаем из карточки сообщения.
					КодЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
					ИмяСобытия = НСтр("ru = 'УчетЗадачПользователей.СообщенияОбсуждений'", КодЯзыка);
					ТекстКомментария = НСтр("ru = 'Ошибка при попытке заблокировать объект %1. Работа продолжена. Ошибка: 
					          				|%2'", КодЯзыка);
					КомментарийДляЖурнала = СтрШаблон(ТекстКомментария, ЭтотОбъект.Ссылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, , , КомментарийДляЖурнала);
					
				КонецПопытки;
				ВладелецОбъект = ВладелецСообщения.ПолучитьОбъект();
				ВладелецОбъект.УстановитьПометкуУдаления(ПометкаУдаления);
			КонецЕсли;
		ИначеЕсли ПометкаУдаления = Истина И ВладелецСообщения.ПометкаУдаления <> Истина Тогда
			// Очищаем у сообщений, дочерних к помеченной, ссылку на родительскую - 
			// переставляем на родительское сообщение удаляемой версии
			УстановитьПривилегированныйРежим(Истина);
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	СообщенияОбсуждений.Ссылка КАК Ссылка
				|ИЗ
				|	Справочник.СообщенияОбсуждений КАК СообщенияОбсуждений
				|ГДЕ
				|	СообщенияОбсуждений.Родитель = &Родитель";	
			Запрос.УстановитьПараметр("Родитель", Ссылка);
			Результат = Запрос.Выполнить();
			Если Не Результат.Пустой() Тогда
				Выборка = Результат.Выбрать();
				Пока Выборка.Следующий() Цикл
					Объект = Выборка.Ссылка.ПолучитьОбъект();
					ЗаблокироватьДанныеДляРедактирования(Объект.Ссылка);
					Объект.Родитель = Родитель;
					Объект.Записать();
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ТекстСообщения <> ТекстСообщенияВИБ() Тогда
		
		// Только в случае изменения текста сообщения меняем дату сообщения и просмотр
		ДополнительныеСвойства.Вставить("ОбновленоСообщение", Истина);
		ДатаИзменения = ТекущаяДатаСеанса();
		Наименование = РаботаСОбсуждениями.ПолучитьЗаголовок(ТекстСообщения);
		УстановитьТекстСписка();
		
	КонецЕсли;
	
	Если Не ЭтоНовый() И ПервоеСообщениеТемы
		И (ЗначениеЗаполнено(РабочаяГруппаДобавить) Или ЗначениеЗаполнено(РабочаяГруппаУдалить)) Тогда
		
		ВладелецОбъект = ВладелецСообщения.ПолучитьОбъект();
		
		Если ЗначениеЗаполнено(РабочаяГруппаДобавить) Тогда
			ВладелецОбъект.ДополнительныеСвойства.Вставить("РабочаяГруппаДобавить", РабочаяГруппаДобавить);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(РабочаяГруппаУдалить) Тогда
			ВладелецОбъект.ДополнительныеСвойства.Вставить("РабочаяГруппаУдалить", РабочаяГруппаУдалить);
		КонецЕсли;
		
		ВладелецОбъект.Записать();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ОбновленоСообщение")
			И ДополнительныеСвойства.ОбновленоСообщение Тогда	
		РаботаСПрочтениямиВызовСервера.УстановитьОбъектКакНеПрочитанный(Ссылка);	
	КонецЕсли;
	
	РаботаСПрочтениямиВызовСервера.УстановитьСвойствоПрочтен(Родитель, , Ложь);
	РаботаСПрочтениямиВызовСервера.УстановитьСвойствоПрочтен(Ссылка);
	
	РаботаСОбсуждениями.ОбновитьИнформациюОТеме(ВладелецСообщения);
	
	// Формирование и отправка уведомления по электронной почте.
	Если ДополнительныеСвойства.Свойство("ФормироватьУведомлениеПоЭлектроннойПочте") Тогда
	 	ФормироватьУведомлениеПоЭлектроннойПочте = ДополнительныеСвойства.ФормироватьУведомлениеПоЭлектроннойПочте;
	Иначе
		ФормироватьУведомлениеПоЭлектроннойПочте = Ложь;
	КонецЕсли;
	
	Если ПометкаУдаления Тогда
	    ФормироватьУведомлениеПоЭлектроннойПочте = Ложь;
	КонецЕсли;
	
	Если ФормироватьУведомлениеПоЭлектроннойПочте Тогда
		// запись должна быть только по команде "Записать"
		ЗадачаПользователя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка,"ВладелецСообщения.Документ");
		
		Если ЗначениеЗаполнено(ЗадачаПользователя) 
			И  ТипЗнч(ЗадачаПользователя) = Тип("ЗадачаСсылка.ЗадачиПользователя") Тогда
			
			МассивЗадач = Новый Массив;
			МассивЗадач.Добавить(ЗадачаПользователя);
			
			ПараметрыУведомленияЗадач = Новый Структура;
			ПараметрыУведомленияЗадач.Вставить("ОтправитьИнициатору", Ложь);
			ПараметрыУведомленияЗадач.Вставить("ОтправитьИсполнителю", Ложь);
			ПараметрыУведомленияЗадач.Вставить("ОтправитьНаEvernote", Ложь);
			ПараметрыУведомленияЗадач.Вставить("ДобавлятьВложение", Ложь);
			
			ПараметрыОбсуждения = Новый Структура;
			ПараметрыОбсуждения.Вставить("ТекущееСообщение",Ссылка);
			ПараметрыОбсуждения.Вставить("ОтправитьОбсуждениеРабочейГруппе", ОтправитьПоПочтеРабочейГруппе);
			ПараметрыОбсуждения.Вставить("ОтправитьОбсуждениеИсполнителю", ОтправитьПоПочтеИсполнителю);
			ПараметрыОбсуждения.Вставить("ОтправитьОбсуждениеПроизвольномуПользователю", ОтправитьПоПочтеПользователю);
			ПараметрыОбсуждения.Вставить("ДобавлятьВложение", Истина);
			ПараметрыОбсуждения.Вставить("ОтправитьДополнительноУведомлениеПоТелеграм", ОтправитьНаТелеграм);
			
			РаботаСЗадачамиВызовСервера.ОтправитьСообщениеПоЗадачамПользователям(МассивЗадач, ПараметрыУведомленияЗадач, ПараметрыОбсуждения);
			
		КонецЕсли; // ЗначениеЗаполнено(ЗадачаПользователя) и  ТипЗнч(ЗадачаПользователя) = Тип("ЗадачаСсылка.ЗадачиПользователя")
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстСообщенияВИБ()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СообщенияОбсуждений.ТекстСообщения
		|ИЗ
		|	Справочник.СообщенияОбсуждений КАК СообщенияОбсуждений
		|ГДЕ
		|	СообщенияОбсуждений.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
    	Возврат Неопределено;
	КонецЕсли;	
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.ТекстСообщения;
	
КонецФункции

Процедура ИзменитьАвтораТемы(Тема)
	
	УстановитьПривилегированныйРежим(Истина);
	Автор = Пользователи.ТекущийПользователь();
	
	ТемаОбъект = Тема.ПолучитьОбъект();
	Если Автор <> ТемаОбъект.Автор Тогда
		ТемаОбъект.Заблокировать();
		ТемаОбъект.Автор = Автор;
		ТемаОбъект.Записать();
	КонецЕсли;
	
КонецПроцедуры

Функция ЕстьПервоеСообщенияТемы(Тема)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СообщенияОбсуждений.Ссылка
		|ИЗ
		|	Справочник.СообщенияОбсуждений КАК СообщенияОбсуждений
		|ГДЕ
		|	СообщенияОбсуждений.ВладелецСообщения = &Тема
		|	И СообщенияОбсуждений.ПервоеСообщениеТемы = ИСТИНА";

	Запрос.УстановитьПараметр("Тема", Тема);

	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
    Выборка.Следующий();
	Если ЗначениеЗаполнено(Выборка.Ссылка) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#КонецЕсли