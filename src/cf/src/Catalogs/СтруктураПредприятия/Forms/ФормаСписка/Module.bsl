

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СписокПользователей.Параметры.УстановитьЗначениеПараметра("Подразделение", ПредопределенноеЗначение("Справочник.СтруктураПредприятия.ПустаяСсылка"));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	Если Элементы.Список.ТекущиеДанные <> Неопределено  Тогда 
		СписокПользователей.Параметры.УстановитьЗначениеПараметра("Подразделение", Элементы.Список.ТекущиеДанные.Ссылка);
	Иначе
		СписокПользователей.Параметры.УстановитьЗначениеПараметра("Подразделение", ПредопределенноеЗначение("Справочник.СтруктураПредприятия.ПустаяСсылка"));		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура СписокПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	Если ПараметрыПеретаскивания.Значение.Количество()>0 Тогда
		
		ОбработкаПеретаскиванияСписка(ПараметрыПеретаскивания.Значение,Строка);
		
	КонецЕсли;
	
КонецПроцедуры

// Перенос пользователя из одного подразделения в другое
//
// Параметры:
//  МассивДанных		 - Массив - массив ссылок СправочникСсылка.ИменаПользователей  
//  НовоеПодразделение	 - СправочникСсылка.СтруктураПредприятия 
//
&НаСервереБезКонтекста
Процедура ОбработкаПеретаскиванияСписка(МассивДанных, НовоеПодразделение) 
	
	Для каждого ТекСтрокаМассива Из МассивДанных Цикл
		
		Если ТипЗнч(ТекСтрокаМассива) = Тип("СправочникСсылка.ИменаПользователей") 
			И ТекСтрокаМассива.Подразделение > НовоеПодразделение Тогда
			
			// изменяем подразделение пользователя
			ОбъектПользователь = ТекСтрокаМассива.ПолучитьОбъект();
			ОбъектПользователь.Заблокировать();
			ОбъектПользователь.Подразделение = НовоеПодразделение; 
			ОбъектПользователь.Записать();  // АПК:566 Нет ошибки синхронного вызова 
			
		КонецЕсли;	
		
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьПользователяИБ(Команда)
	
	СписокПользователейДанных = Новый СписокЗначений;
	
	Если Элементы.СписокПользователей.ТекущиеДанные <> Неопределено  Тогда 
		Для каждого ТекСтрока Из Элементы.СписокПользователей.ВыделенныеСтроки Цикл
			
			СписокПользователейДанных.Добавить(ТекСтрока);

		КонецЦикла;
	КонецЕсли;
	
	ПараметрыДанных = Новый Структура;
	ПараметрыДанных.Вставить("Пользователи",СписокПользователейДанных);
	ПараметрыДанных.Вставить("ГруппаДоступа",ПредопределенноеЗначение("Справочник.ГруппыДоступа.ПустаяСсылка"));
	
	ОткрытьФорму("Справочник.СтруктураПредприятия.Форма.СозданиеПользователяБазы",ПараметрыДанных);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьФайлыНастроекСпискаБазПользователей(Команда)
	
	Если Элементы.СписокПользователей.ТекущиеДанные = Неопределено  Тогда 
		Возврат;	
	КонецЕсли;
	
	Для каждого ТекПользователь Из Элементы.СписокПользователей.ВыделенныеСтроки Цикл
		
		ОбновитьФайлыНастроекСпискаБазПользователейНаСервере(ТекПользователь);
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Обновление настроек списка баз пользователя %1 завершено.'"), ТекПользователь);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);

	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьФайлыНастроекСпискаБазПользователейНаСервере(ПользовательСсылка)
	
	РаботаСоСтруктуройКаталоговAD.ОбновитьФайлНастроекСпискаБазПользователя(ПользовательСсылка);
	
КонецПроцедуры

#КонецОбласти




