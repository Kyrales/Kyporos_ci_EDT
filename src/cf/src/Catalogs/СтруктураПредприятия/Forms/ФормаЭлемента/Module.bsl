

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОтображениеИнформацииНаФорме();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ВыбораГруппыAD_СтруктураПредприятия" Тогда
		Объект.ГруппаAD = Параметр;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОтображениеИнформацииНаФорме();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РодительПриИзменении(Элемент)
	
	ОтображениеИнформацииНаФорме();
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаADНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("ДеревоADФормы", ДеревоADФормы);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Справочник.СтруктураПредприятия.Форма.ВыборГруппыAD", ПараметрыФормы);	
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаADПриИзменении(Элемент)
	
	Если Объект.ГруппаAD="" Тогда
		// очистка организации
		Объект.Организация = "";
	КонецЕсли;
	
	ОтображениеИнформацииНаФорме();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Отображение информации на форме по определенным условиям
&НаКлиенте
Процедура ОтображениеИнформацииНаФорме() 
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		// для новых недоступны для правки настройки
		Элементы.ДекорацияИнформацияОНастройках.Видимость = Истина;
		Элементы.ГруппаНастроек.Доступность = Ложь;
		Возврат;
	Иначе
		Элементы.ДекорацияИнформацияОНастройках.Видимость = Ложь;
		Элементы.ГруппаНастроек.Доступность = Истина;
	КонецЕсли;
	
	Элементы.ГруппаСоответствиеСтруктуреAD.Видимость = Не ПринадлежностьКСлужебнойПапке(Объект.Ссылка);
	
	// Только если заполнена группа принадлежности к AD, можно выбирать организацию для заполнения.
	Элементы.Организация.Видимость = ЗначениеЗаполнено(Объект.ГруппаAD);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПринадлежностьКСлужебнойПапке(СтруктураПредприятияСсылка) 
	
	Если Не ЗначениеЗаполнено(СтруктураПредприятияСсылка) Тогда
		Возврат Истина;	
	КонецЕсли;
	
	Если СтруктураПредприятияСсылка.ПринадлежитЭлементу(Справочники.СтруктураПредприятия.СлужебнаяПапка) 
		Или СтруктураПредприятияСсылка = Справочники.СтруктураПредприятия.СлужебнаяПапка 
		Или СтруктураПредприятияСсылка.ПринадлежитЭлементу(Справочники.СтруктураПредприятия.ИсторияПредприятия) 
		Или СтруктураПредприятияСсылка = Справочники.СтруктураПредприятия.ИсторияПредприятия Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти








