
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов	
	
	Элементы.КнопкаЦветФона.ЦветФона = ?(ЗначениеЗаполнено(Объект.ЦветФонаСостояния), ЗначениеИзСтрокиВнутр(Объект.ЦветФонаСостояния), ЦветаСтиля.ЦветФонаКнопки);	

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Изменено_СостояниеЗадачи", Объект.Ссылка, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьЦветФона(Команда)
	
	ДиалогЦвета = Новый ДиалогВыбораЦвета();
	
	ДиалогЦвета.Цвет = Элементы.КнопкаЦветФона.ЦветФона;
	ДиалогЦвета.Показать(Новый ОписаниеОповещения("ИзменитьЦветФонаЗавершение", ЭтотОбъект, Новый Структура("ДиалогЦвета", ДиалогЦвета)));

КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЦветФонаЗавершение(Цвет, ДополнительныеПараметры) Экспорт
	
	ДиалогЦвета = ДополнительныеПараметры.ДиалогЦвета;
	Если (Цвет <> Неопределено) = Истина Тогда
		ИзменитьЦветФонаНаСервере(ДиалогЦвета.Цвет);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОчиститьЦвет(Команда)
	
	ОчиститьЦветСервер();
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьЦветСервер()

	Объект.ЦветФонаСостояния = "";
	
	Элементы.КнопкаЦветФона.ЦветФона = ЦветаСтиля.ЦветФонаКнопки;	

КонецПроцедуры // ОчиститьЦветСервер()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура изменяет устанавливает в значение реквизита - цвет фона.
//
&НаСервере
Процедура ИзменитьЦветФонаНаСервере(Цвет)
	
	Объект.ЦветФонаСостояния = ЗначениеВСтрокуВнутр(Цвет);
	Элементы.КнопкаЦветФона.ЦветФона = Цвет;
	
КонецПроцедуры

#КонецОбласти
