
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПользовательОценщик = Пользователи.ТекущийПользователь();
	
	ЗаполнитьСписокЗадач(ПользовательОценщик);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОценкаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.СписокЗадач.ТекущиеДанные;
	
	Если ТекущаяСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока.Оценка = Оценка;	
	ТекущаяСтрока.ВыполненаОценка = Истина;
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокЗадач

&НаКлиенте
Процедура СписокЗадачПриАктивизацииСтроки(Элемент)
	
	Оценка = 0;

КонецПроцедуры

&НаКлиенте
Процедура СписокЗадачВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущаяСтрока = Элементы.СписокЗадач.ТекущиеДанные;
	
	Если ТекущаяСтрока = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ПоказатьЗначение(,ТекущаяСтрока.Задача);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодтвердитьВыбранныеОценки(Команда)
	
	ПодтвердитьВыбранныеОценкиНаСервере();
	
	Оповестить("Запись_ЗадачаИсполнителя",,ЭтаФорма);
	
	ПоказатьОповещениеПользователя(
			НСтр("ru = 'Оценка выполнения задач подтверждена.'"));
	
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура ПодтвердитьВыбранныеОценкиНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для каждого ТекСтрокаСпискаЗадач Из СписокЗадач Цикл
		
		Если ТекСтрокаСпискаЗадач.Оценка=0 
			Или (Не ТекСтрокаСпискаЗадач.Задача.Выполнена) Тогда
			Продолжить;
		КонецЕсли;
		
		ОбъектДанных = ТекСтрокаСпискаЗадач.Задача.ПолучитьОбъект();
		ОбъектДанных.ОценкаЗадачи	  = ТекСтрокаСпискаЗадач.Оценка;
		ОбъектДанных.ДатаОценкиЗадачи = ТекущаяДата();
		ОбъектДанных.Записать();
		
		Комментарий = НСтр("ru = 'Оценена задача на %1'");
		Комментарий = СтрШаблон(Комментарий, ТекСтрокаСпискаЗадач.Оценка);
		
		ИсторияСобытийЗадачВызовСервера.ЗаписатьСобытие(
			ОбъектДанных.Ссылка, 
			Перечисления.ВидыСобытийЗадач.ОценкаВыполнения,
			Комментарий);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокЗадач(ПользовательОценщик)
	
	Выборка = РаботаСЗадачами.ПолучитьСписокЗадачНаОценку(ПользовательОценщик);
	
	СписокЗадач.Загрузить(Выборка);
	
КонецПроцедуры // ЗаполнитьСписокЗадач()

&НаКлиенте
Процедура ОбновитьСписокЗадач(Команда)
	
	 ЗаполнитьСписокЗадач(ПользовательОценщик);
	
КонецПроцедуры

#КонецОбласти



