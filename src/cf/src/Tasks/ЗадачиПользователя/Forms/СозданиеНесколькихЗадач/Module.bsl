
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Важность = Перечисления.ВариантыВажностиЗадачи.Обычная;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьЗадачи(Команда)
	
	СоздатьЗадачиНаСервере();
	
	Оповестить("Запись_ЗадачаИсполнителя",,ЭтаФорма);
	
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура СоздатьЗадачиНаСервере()
	
	Для каждого ТекСтрока Из Исполнители Цикл
	
		Если ТипЗнч(ТекСтрока.Значение) = Тип("СправочникСсылка.ГруппыПользователей")  Тогда
			
			Для каждого ТекСтрокаИсполнители Из ТекСтрока.Значение.Состав Цикл
				
				Если Не ЗначениеЗаполнено(ТекСтрокаИсполнители.Пользователь) Тогда
					Продолжить;
				КонецЕсли;
				
				СоздатьЗадачу(ТекСтрокаИсполнители.Пользователь);
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Создана задача для: %1'"), ТекСтрокаИсполнители.Пользователь);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
				
			КонецЦикла;
			
		Иначе
			
			Если Не ЗначениеЗаполнено(ТекСтрока.Значение) Тогда
				Продолжить;	
			КонецЕсли;
			
			СоздатьЗадачу(ТекСтрока.Значение);
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Создана задача для: %1'"), ТекСтрока.Значение);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			
		КонецЕсли;  // ТипЗнч(ТекСтрока.Значение) = Тип("СправочникСсылка.ГруппыПользователей")
	
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СоздатьЗадачу(Исполнитель)

	ОбъектЗадача = Задачи.ЗадачиПользователя.СоздатьЗадачу();	
	
	ОбъектЗадача.ДатаНачала = ТекущаяДата();
	ОбъектЗадача.Инициатор  = Пользователи.ТекущийПользователь();
	ОбъектЗадача.ПолучательУслуг = Пользователи.ТекущийПользователь();
	ОбъектЗадача.Проект = Проект;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьВидыЗадач") Тогда
		ОбъектЗадача.ВидЗадачи = ВидЗадачи;
	КонецЕсли;
	
	ОбъектЗадача.Наименование = Наименование;
	ОбъектЗадача.Описание = Описание;
	ОбъектЗадача.Важность = Важность;
	ОбъектЗадача.СрокИсполнения = СрокИсполнения;
	
	ОбъектЗадача.Исполнитель = Исполнитель;
	
	ОбъектЗадача.Записать();
	
	
	// Установка состояния в Зарегистрирована.
	РегистрыСведений.ТекущиеСостоянияЗадач.ОбновитьТекущееСостояниеЗадачи(ОбъектЗадача.Ссылка, Справочники.СостоянияЗадач.Зарегистрирована);
	
КонецПроцедуры // СоздатьЗадачу()

#КонецОбласти
