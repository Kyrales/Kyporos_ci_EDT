
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ПараметрКоманды = Неопределено Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Не выбраны задачи.'"));
		Возврат;
	КонецЕсли;
		
	ОчиститьСообщения();
	
	Оповещение = Новый ОписаниеОповещения("ОбработкаКомандыЗавершение",ЭтотОбъект,ПараметрКоманды);
	ПоказатьВопрос(Оповещение, "Задача выполнена?", РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаКомандыЗавершение(Результат, ПараметрКоманды) Экспорт

	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	Для Каждого Задача Из ПараметрКоманды Цикл
		
		// проверка на наличие проекта и выбор его в случае отсутствия
		ПроверитьНаличиеПроекта(Задача,Отказ);
		Если Отказ Тогда
			Отказ = Ложь;
			Продолжить;
		КонецЕсли;
		
		// выполнение задачи
		ВыполнитьЗадачу(Задача, Истина);
		
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Задача выполнена'"),
			ПолучитьНавигационнуюСсылку(Задача),
			Строка(Задача));
			
	КонецЦикла;
	
	Оповестить("Запись_ЗадачаИсполнителя", Новый Структура("Выполнена", Истина), ПараметрКоманды);

КонецПроцедуры // ОбработкаКомандыЗавершение()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВыполнитьЗадачу(ЗадачаСсылка, ДействиеПоУмолчанию = Ложь)

	Задачи.ЗадачиПользователя.ВыполнитьЗадачу(ЗадачаСсылка, ДействиеПоУмолчанию); 	

КонецПроцедуры // ()

&НаКлиенте
Процедура ПроверитьНаличиеПроекта(ЗадачаСсылка,Отказ = Ложь)
	
	ПроектСсылка = ПроектСсылкаИзЗадачи(ЗадачаСсылка);
	
	Если Не ПроектСсылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Если РаботаСЗадачамиВызовСервера.ЭтоПолноправныйПользовательЗадач() Тогда
		ПоказатьПредупреждение(, НСтр("ru='Не выбран проект у выполняемой задачи!'"));		
		Отказ = Истина;
	КонецЕсли;	
	
КонецПроцедуры // ПроверитьНаличиеПроекта()

&НаСервере
Функция ПроектСсылкаИзЗадачи(ЗадачаСсылка)

	Возврат ЗадачаСсылка.Проект;

КонецФункции // ПроектСсылкаИзЗадачи()

#КонецОбласти
