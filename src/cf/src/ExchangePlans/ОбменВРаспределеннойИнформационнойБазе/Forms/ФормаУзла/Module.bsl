
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Объект.ЭтотУзел Тогда
		Элементы.НастройкаФильтровРегистрации.Видимость = Ложь;
	КонецЕсли;
	
	// { *Олег Каратаев [07.01.2020]
	ВсеНастройкиВыполнены = РегистрыСведений.ОбщиеНастройкиУзловИнформационныхБаз.НастройкаЗавершена(Объект.Ссылка);
	// }
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	Оповестить("Запись_УзелПланаОбмена");
	
КонецПроцедуры

&НаКлиенте
Процедура ВсеНастройкиВыполненыПриИзменении(Элемент)
	
	СтруктураЗаписи = Новый Структура;
	СтруктураЗаписи.Вставить("УзелИнформационнойБазы", Объект.Ссылка);
	СтруктураЗаписи.Вставить("НастройкаЗавершена",     ВсеНастройкиВыполнены);
	
	ОбновитьЗаписьНастроек(СтруктураЗаписи);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбновитьЗаписьНастроек(СтруктураЗаписи)

	ОбменДаннымиСлужебный.ОбновитьЗаписьВРегистрСведений(СтруктураЗаписи, "ОбщиеНастройкиУзловИнформационныхБаз");

КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторУзлаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ЗаблокироватьДанныеФормыДляРедактирования();
	
	ПоказатьВводСтроки(Новый ОписаниеОповещения("ИдентификаторУзлаНажатиеЗавершение", ЭтотОбъект), Строка(Объект.ИдентификаторУзла),НСтр("ru = 'Изменить идентификатор узла обмена'"));        
	
КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторУзлаНажатиеЗавершение(Значение, ДополнительныеПараметры) Экспорт
	
	Если Значение = Неопределено 
		Или Строка(Объект.ИдентификаторУзла) = Значение Тогда
		// изменения не было
		Возврат;
	КонецЕсли;
	
	Объект.ИдентификаторУзла = Новый УникальныйИдентификатор(Значение);
	
	ЭтаФорма.Модифицированность = Истина;

КонецПроцедуры

#КонецОбласти
