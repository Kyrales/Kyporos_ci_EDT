
#Область ПрограммныйИнтерфейс

// Сохранение описания изменений конфигурации, по выбранной версии в стиле стандартного 1С
//
// Параметры:
//  Конфигурация  - СправочникСсылка.Конфигурации
//  Версия  - СправочникСсылка.ВерсииКонфигураций
//  ПолнаяИсторияОписанияВерсии  - Булево - Истина, полное описание всей версии, иначе только выбранной версии с подверсией
//
Процедура СохранитьHTMLДокументОписанияИзмененийКонфигурации(Конфигурация, Версия, ПолнаяИсторияОписанияВерсии = Истина) Экспорт

	ДанныеПоКонфигурации = УправлениеУчетомКонфигурацийВызовСервера.ПолучитьДанныеПоКонфигурации(Конфигурация, Версия, ПолнаяИсторияОписанияВерсии);
	
	ВыборФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ВыборФайла.ПолноеИмяФайла = СтрШаблон(НСтр("ru = '%1. Версия %2. Новое в версии'"), Конфигурация, Версия);
	ВыборФайла.Фильтр = "HTML (*.htm)|*.htm";
	
	ДополнительныеПараметры  = Новый Структура;
	ДополнительныеПараметры.Вставить("Конфигурация", Конфигурация);
	ДополнительныеПараметры.Вставить("Версия", Версия);
	ДополнительныеПараметры.Вставить("ПолнаяИсторияОписанияВерсии", ПолнаяИсторияОписанияВерсии);
	ДополнительныеПараметры.Вставить("ДанныеПоКонфигурации", ДанныеПоКонфигурации);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("СохранитьHTMLДокументОписанияИзмененийКонфигурацииЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ФайловаяСистемаКлиент.ПоказатьДиалогВыбора(ОписаниеОповещения, ВыборФайла);
		
КонецПроцедуры // СохранитьHTMLДокументОписанияИзмененийКонфигурации()

// Сохранение HTML-Документа, с описанием изменений конфигурации
//
// Параметры:
//  СписокИменФайлов		 - Массив из Строка - полный путь к файлу  
//  ДополнительныеПараметры	 - Структура:
//		* ДанныеПоКонфигурации - см. УправлениеУчетомКонфигурацийВызовСервера.ПолучитьДанныеПоКонфигурации 
//  	* Конфигурация  - СправочникСсылка.Конфигурации
//  	* Версия  - СправочникСсылка.ВерсииКонфигураций
//  	* ПолнаяИсторияОписанияВерсии - Булево - Истина, полное описание всей версии, иначе только выбранной версии с подверсией
//
Процедура СохранитьHTMLДокументОписанияИзмененийКонфигурацииЗавершение(СписокИменФайлов, ДополнительныеПараметры) Экспорт
	
	Если СписокИменФайлов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПолноеИмяФайла = СписокИменФайлов[0];
	ДанныеПоКонфигурации = ДополнительныеПараметры.ДанныеПоКонфигурации;
	Конфигурация = ДополнительныеПараметры.Конфигурация;
	Версия = ДополнительныеПараметры.Версия;
	ПолнаяИсторияОписанияВерсии = ДополнительныеПараметры.ПолнаяИсторияОписанияВерсии;
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	
	ТекстовыйДокумент.УстановитьТекст("<HTML xmlns:o = ""urn:schemas-microsoft-com:office:office"" 
		| xmlns:st1 = ""urn:schemas-microsoft-com:office:smarttags""><HEAD>
		|<META content=""text/html; charset=utf-8"" http-equiv=Content-Type><LINK rel=stylesheet type=text/css href=""__STYLE__"">
		|<META name=GENERATOR content=""MSHTML 9.00.8112.16457""></HEAD>
		|<BODY>
		|<TABLE style=""BORDER-BOTTOM: black 1px solid; BORDER-LEFT: black 1px solid; BORDER-TOP: black 1px solid; BORDER-RIGHT: black 1px solid"" border=0 cellSpacing=0 cellPadding=8 width=""100%"" bgColor=#ffffd6>
		|<TBODY>
		|<TR>
		|<TD>
		|<H1><FONT color=#823602 face=Verdana>1С:Предприятие 8</FONT></H1>
		|<H2><FONT color=#c10000 face=Tahoma>Описание изменений конфигурации<BR>" + Строка(Конфигурация) 
			+ ", редакция " + ДанныеПоКонфигурации.Редакция + "</FONT> </H2></TD></TR></TBODY></TABLE>
		|<H3 style=""BORDER-BOTTOM: rgb(193,0,0) 2px solid""><FONT color=#c10000 size=5 face=Arial><EM>Версия " 
			+ УправлениеУчетомКонфигурацийВызовСервера.ПолучитьВерсиюКонфигурации(ДанныеПоКонфигурации.ПоследняяВерсияКонфигурации) 
			+ "</EM></FONT></H3>");
	
	Для каждого СтрокаМассиваВерсии Из ДанныеПоКонфигурации.ОписаниеИзменений Цикл
	      // обход по версии
		  
		  Для каждого СтрокаМассиваОписанияИзменения Из СтрокаМассиваВерсии.ОписаниеИзмененийВерсии Цикл
		  
		  	   ТекстовыйДокумент.ДобавитьСтроку(СтрокаМассиваОписанияИзменения);
		  
		  КонецЦикла;
	
	КонецЦикла;	
	
	ТекстовыйДокумент.ДобавитьСтроку("</BODY></HTML>");
	
	ТекстовыйДокумент.Записать(ПолноеИмяФайла);
	
КонецПроцедуры 

#КонецОбласти

