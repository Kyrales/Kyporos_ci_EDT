
#Область ПрограммныйИнтерфейс

// Подключение к рабочему месту по основной схеме
//
// Параметры:
//  РабочееМесто - СправочникСсылка.РабочиеМеста 
//
Процедура ПодключениеКРабочемуМестуПоОсновнойСхеме(РабочееМесто) Экспорт
	
	Если ТипЗнч(РабочееМесто) = Тип("СправочникСсылка.РабочиеМеста") Тогда
		ШаблонУдаленногоДоступа = УдаленноеУправлениеВызовСервера.ОсновнойШаблонУдаленногоДоступа(РабочееМесто);
		Если ШаблонУдаленногоДоступа.Пустая() Тогда
			ПодключениеКРабочемуМесту(РабочееМесто,УдаленноеУправлениеВызовСервера.ШаблонУдаленногоПодключенияПоУмолчанию());
		Иначе	 
			ПодключениеКРабочемуМесту(РабочееМесто,ШаблонУдаленногоДоступа);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - Подключение к рабочему месту
//
// Параметры:
//  РабочееМесто - СправочникСсылка.РабочиеМеста 
//  ШаблонПодключения	 - СправочникСсылка.ШаблоныУдаленногоДоступа 
//
Процедура ПодключениеКРабочемуМесту(РабочееМесто, ШаблонПодключения) Экспорт
	
	Перем СтрокаКоманды, Счетчик, ТекСтрока;
	
	Если ТипЗнч(ШаблонПодключения) = Тип("СправочникСсылка.ШаблоныУдаленногоДоступа") 
		И ТипЗнч(РабочееМесто) = Тип("СправочникСсылка.РабочиеМеста") Тогда
		
		СтрокаКоманды = УдаленноеУправлениеВызовСервера.ПолучитьТекстКоманднойСтроки(РабочееМесто, ШаблонПодключения);
		
		Для Счетчик = 1 По СтрЧислоСтрок(СтрокаКоманды) Цикл
			
			ТекСтрока = СтрПолучитьСтроку(СтрокаКоманды, Счетчик);
			
			Если СокрЛП(ТекСтрока) = "" Тогда
				Продолжить;
			КонецЕсли;
			
			ПараметрыЗапуска = ФайловаяСистемаКлиент.ПараметрыЗапускаПрограммы();
			ПараметрыЗапуска.ТекущийКаталог = УдаленноеУправлениеВызовСервера.ШаблонПодключенияРабочийКаталог(ШаблонПодключения);
			ФайловаяСистемаКлиент.ЗапуститьПрограмму(ТекСтрока, ПараметрыЗапуска);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


