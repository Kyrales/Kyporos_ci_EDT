
#Область ПрограммныйИнтерфейс

// Данные об активах в регистре УчетОборудованияРабочихМест
//
// Параметры:
//  ДанныеДляПоиска	 - Структура - содержит свойства:
//		* Организация - СправочникСсылка.Организации - может быть не заполнено
//		* РабочееМесто - СправочникСсылка.РабочиеМеста - может быть не заполнено
//		* Номенклатура - СправочникСсылка.Номенклатура - может быть не заполнено
//		* ВидНоменклатуры - СправочникСсылка.ВидыНоменклатуры - может быть не заполнено
// 
// Возвращаемое значение:
//  Структура - содержит свойства:
//		* Организация - СправочникСсылка.Организации
//		* РабочееМесто - СправочникСсылка.РабочиеМеста
//		* Номенклатура - СправочникСсылка.Номенклатура
//		* Количество - Число
//
Функция ДанныеОбАктивах(ДанныеДляПоиска) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	УчетОборудованияРабочихМест.Организация,
	               |	УчетОборудованияРабочихМест.РабочееМесто,
	               |	УчетОборудованияРабочихМест.Номенклатура,
	               |	УчетОборудованияРабочихМест.Количество,
	               |	УчетОборудованияРабочихМест.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры
	               |ИЗ
	               |	РегистрСведений.УчетОборудованияРабочихМест КАК УчетОборудованияРабочихМест
	               |ГДЕ
	               |	ВЫБОР
	               |			КОГДА &ЕстьОрганизация
	               |				ТОГДА УчетОборудованияРабочихМест.Организация = &Организация
	               |			ИНАЧЕ ИСТИНА
	               |		КОНЕЦ
	               |	И ВЫБОР
	               |			КОГДА &ЕстьРабочееМесто
	               |				ТОГДА УчетОборудованияРабочихМест.РабочееМесто = &РабочееМесто
	               |			ИНАЧЕ ИСТИНА
	               |		КОНЕЦ
	               |	И ВЫБОР
	               |			КОГДА &ЕстьНоменклатура
	               |				ТОГДА УчетОборудованияРабочихМест.Номенклатура = &Номенклатура
	               |			ИНАЧЕ ИСТИНА
	               |		КОНЕЦ
	               |	И ВЫБОР
	               |			КОГДА &ЕстьВидНоменклатуры
	               |				ТОГДА УчетОборудованияРабочихМест.Номенклатура.ВидНоменклатуры = &ВидНоменклатуры
	               |			ИНАЧЕ ИСТИНА
	               |		КОНЕЦ";
	
	
	Запрос.УстановитьПараметр("ЕстьОрганизация", ДанныеДляПоиска.Свойство("Организация"));		
	Запрос.УстановитьПараметр("Организация", Справочники.Организации.ПустаяСсылка());		
	Если ДанныеДляПоиска.Свойство("Организация") Тогда
		Запрос.УстановитьПараметр("Организация", ДанныеДляПоиска.Организация);
	КонецЕсли;
	Запрос.УстановитьПараметр("ЕстьРабочееМесто", ДанныеДляПоиска.Свойство("РабочееМесто"));				
	Запрос.УстановитьПараметр("РабочееМесто", Справочники.РабочиеМеста.ПустаяСсылка());
	Если ДанныеДляПоиска.Свойство("РабочееМесто") Тогда
		Запрос.УстановитьПараметр("РабочееМесто", ДанныеДляПоиска.РабочееМесто);
	КонецЕсли;
	Запрос.УстановитьПараметр("ЕстьНоменклатура", ДанныеДляПоиска.Свойство("Номенклатура"));				
	Запрос.УстановитьПараметр("Номенклатура", Справочники.Номенклатура.ПустаяСсылка());
	Если ДанныеДляПоиска.Свойство("Номенклатура") Тогда
		Запрос.УстановитьПараметр("Номенклатура", ДанныеДляПоиска.Номенклатура);
	КонецЕсли;
	Запрос.УстановитьПараметр("ЕстьВидНоменклатуры", ДанныеДляПоиска.Свойство("ВидНоменклатуры"));				
	Запрос.УстановитьПараметр("ВидНоменклатуры", Справочники.ВидыНоменклатуры.ПустаяСсылка());
	Если ДанныеДляПоиска.Свойство("ВидНоменклатуры") Тогда
		Запрос.УстановитьПараметр("ВидНоменклатуры", ДанныеДляПоиска.ВидНоменклатуры);
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	СтруктураНайденныхДанных = Новый Структура();
	СтруктураНайденныхДанных.Вставить("Организация" ,Справочники.Организации.ПустаяСсылка());	
	СтруктураНайденныхДанных.Вставить("РабочееМесто",Справочники.РабочиеМеста.ПустаяСсылка());	
	СтруктураНайденныхДанных.Вставить("Номенклатура",Справочники.Номенклатура.ПустаяСсылка());	
	СтруктураНайденныхДанных.Вставить("Количество",0);	
	
	Если Выборка.Следующий() Тогда
	
		 СтруктураНайденныхДанных.Организация  = Выборка.Организация;
		 СтруктураНайденныхДанных.РабочееМесто = Выборка.РабочееМесто;
		 СтруктураНайденныхДанных.Номенклатура = Выборка.Номенклатура;
		 
		 СтруктураНайденныхДанных.Количество   = Выборка.Количество;
		 
	КонецЕсли;
	
    Возврат СтруктураНайденныхДанных;
	
КонецФункции // ПолучитьДанныеОбАктивах()

#КонецОбласти

