
#Область ПрограммныйИнтерфейс

Функция УстановитьСвязь(ИмяФайла, КоличествоЛистов = 1) Экспорт
	
	Результат = Неопределено;
	
	#Если Не МобильныйКлиент И Не МобильноеПриложениеКлиент И Не МобильноеПриложениеСервер Тогда
	Попытка
		
		Результат = Новый Соответствие;
		
		BaseOLE = Новый COMОбъект("Excel.Application");
		Результат.Вставить("Excel", BaseOLE);
		
		ExcelФайл = BaseOLE.WorkBooks.Open(ИмяФайла);
		Результат.Вставить("ExcelФайл", ExcelФайл);
		
		Для сч = 1 По КоличествоЛистов Цикл
			
			Результат.Вставить("WS" + сч, ExcelФайл.Sheets(сч));
			
		КонецЦикла;

	Исключение
		
		Результат = Неопределено;
		
		ТекстСообщения = НСтр("ru = 'Ошибка создания обьекта Microsoft Excel:%1'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, Символы.ПС + ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
	КонецПопытки;
	#КонецЕсли
	
	Возврат Результат;
	
КонецФункции

Функция РазорватьСвязь(СоединениеExcel) Экспорт
	
	Если Не ЗначениеЗаполнено(СоединениеExcel) Тогда
		
		Возврат Истина;
		
	КонецЕсли;
	
	Попытка
		
		СоединениеExcel["Excel"].DisplayAlerts = 0;
		СоединениеExcel["ExcelФайл"].Close();
		СоединениеExcel["Excel"].DisplayAlerts = 1;
		СоединениеExcel["Excel"].Quit();
		
		Для каждого ЭлементКоллекции Из СоединениеExcel Цикл
			// В коллекции кроме объекта Excel может быть произвольное число открытых листов.
			// Обнулим их все.
			
			СоединениеExcel[ЭлементКоллекции.Ключ] = Неопределено;
			
		КонецЦикла;
		
		СоединениеExcel = Неопределено;
		
		Возврат Истина;
		
	Исключение
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		Возврат Ложь;
			
	КонецПопытки;
	
КонецФункции

Функция УстановитьЗначениеВЯчейке(Значение, WorkSheet, НомерСтроки, НомерКолонки) Экспорт
	
	Попытка
		
		ЯчейкаДокумента = WorkSheet.Cells(НомерСтроки, НомерКолонки);
		ЯчейкаДокумента.value = Значение;
		
		Возврат Истина;
		
	Исключение
		
		ТекстСообщения = НСтр("ru = 'Не удалось установить значение в строке №%1 колонки %2:%3'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, НомерСтроки, "НомерКолонки", Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
		Возврат Ложь;
		
	КонецПопытки;
	
КонецФункции

Функция СохранитьФайл(СоединениеExcel) Экспорт
	
	DisplayAlerts = СоединениеExcel["Excel"].DisplayAlerts;
	СоединениеExcel["Excel"].DisplayAlerts = False;
	
	Попытка
		
		СоединениеExcel["ExcelФайл"].Save();
		
		Результат = Истина;
		
	Исключение
		
		ТекстСообщения = НСтр("ru = 'Не удалось сохранить файл:%3'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, Символы.ПС + ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
		Результат = Ложь;
		
	КонецПопытки;
	
	СоединениеExcel["Excel"].DisplayAlerts = DisplayAlerts;
	
	Возврат Результат;
	
КонецФункции

// Чтение значение в ячейке Excel
//
// Параметры:
//	ПараметрыЧтения - Структура:
//  	* WorkSheet 
//  	* НомерСтроки 
//  	* НомерКолонки 
//  	* ИмяКолонки 
//  	* ТипКолонки				 
//  ПроверитьКонецФайла		 - Булево - необязательный параметр 
//  РазделительДробнойЧасти	 - Строка - необязательный параметр
//  РазделительГрупп		 - Строка - необязательный параметр
// 
// Возвращаемое значение:
//  - Число
//	- Дата
//	- Строка
//	- Неопределено - если конец файла и значение не заполнено данными
//
Функция ПрочитатьЗначениеВЯчейке(ПараметрыЧтения, ПроверитьКонецФайла = Ложь, РазделительДробнойЧасти = ",", РазделительГрупп = "") Экспорт
	
	WorkSheet = ПараметрыЧтения.WorkSheet;
	НомерСтроки = ПараметрыЧтения.НомерСтроки;
	НомерКолонки = ПараметрыЧтения.НомерКолонки;
	ИмяКолонки = ПараметрыЧтения.ИмяКолонки;
	ТипКолонки = ПараметрыЧтения.ТипКолонки;
	
	Значение = WorkSheet.Cells(НомерСтроки, НомерКолонки).value;
	
	Если ТипЗнч(Значение) = Тип("Строка") Тогда
		
		Значение = СокрЛП(Значение);
		
	КонецЕсли;
	
	Если ПроверитьКонецФайла
		И Не ЗначениеЗаполнено(Значение) Тогда // в файле больше нет значащих строк
	
		Возврат Неопределено;
	
	КонецЕсли;
	
	ШаблонСообщения = НСтр("ru = 'В строке №%1  не удалось получить значение колонки ""%2""'");
	
	Если ТипКолонки = "Число" Тогда
		
		Если ТипЗнч(Значение) = Тип("Строка") Тогда
			
			Значение = СтрЗаменить(Значение, РазделительГрупп, "");
			Значение = СтрЗаменить(Значение, РазделительДробнойЧасти, ",");
			
			Попытка
			
				Значение = Число(Значение);
			
			Исключение
				
				 ТекстСообщения = СтрШаблон(НСтр("ru = 'Ошибка преобразования в число: %1.'"), Значение);
				 ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
				
			КонецПопытки;
			
		КонецЕсли;
		
		Если ТипЗнч(Значение) <> Тип("Число") Тогда
		
			ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон(ШаблонСообщения, НомерСтроки, ИмяКолонки));
		
		КонецЕсли;
		
	ИначеЕсли ТипКолонки = "Дата" Тогда
		
		Если ТипЗнч(Значение) = Тип("Строка") Тогда
			
			ЗначениеПрав = Сред(Значение, 7, 4) + Сред(Значение, 4, 2) + Сред(Значение, 1, 2);
			
			Попытка
				
				Значение = Дата(ЗначениеПрав);
				
			Исключение
				
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Ошибка преобразования в дату: %1.'"), ЗначениеПрав);
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
				
			КонецПопытки;
			
		КонецЕсли;
		
		Если ТипЗнч(Значение) <> Тип("Дата") Тогда
		
			ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон(ШаблонСообщения, НомерСтроки, ИмяКолонки));
		
		КонецЕсли;
		
	ИначеЕсли ТипКолонки = "Строка" Тогда
		
		Если ТипЗнч(Значение) = Тип("Число") Тогда
			
			Значение = Формат(Значение, "ЧГ=0");
			
		Иначе
			
			Попытка
				
				Значение = Строка(Значение);
				
			Исключение
				
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Ошибка преобразования в строку: %1.'"), Значение);
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
				
			КонецПопытки;
			
		КонецЕсли;
	
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

Функция НайтиЗначениеВКолонке(СоединениеExcel, НомерКолонки, Значение, ТипЗначения = "Строка", МаксимумПустыхСтрокПодряд = 50) Экспорт
	
	WorkSheet = СоединениеExcel["WS1"];
	КоличествоСтрок = WorkSheet.Rows.Count;
	
	// ПОИСК ПЕРВОЙ СТРОКИ
	НомерПервойСтроки = Неопределено;
	сч = 0;
	ПустыхСтрокПодряд = 0;
	
	Пока сч <= КоличествоСтрок Цикл
	
		сч = сч + 1;
		
		ПараметрыЧтения = Новый Структура;
		ПараметрыЧтения.WorkSheet = WorkSheet;
		ПараметрыЧтения.НомерСтроки = сч;
		ПараметрыЧтения.НомерКолонки = 1;
		ПараметрыЧтения.ИмяКолонки = НомерКолонки;
		ПараметрыЧтения.ТипКолонки = ТипЗначения;
		
		ЗначениеЯчейки = ПрочитатьЗначениеВЯчейке(ПараметрыЧтения);
		
		Если ЗначениеЗаполнено(ЗначениеЯчейки) Тогда
			
			ПустыхСтрокПодряд = 0;
			
		Иначе
			
			ПустыхСтрокПодряд = ПустыхСтрокПодряд + 1;
			
		КонецЕсли;
		
		Если ПустыхСтрокПодряд > МаксимумПустыхСтрокПодряд Тогда
			
			Прервать;
			
		КонецЕсли;
		
		Если ЗначениеЯчейки = Значение Тогда
			
			НомерПервойСтроки = сч;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат НомерПервойСтроки;
	
КонецФункции

#КонецОбласти

