////////////////////////////////////////////////////////////////////////////////
// МОДУЛЬ ПРОВЕРКИ ОРФОГРАФИИ #Spelling
//  - использован на основе конфигурации 1С:Документооборот КОРП 2.1.7.9. Функции из модуля ВстроеннаяПочтаКлиент
//  - дополнительно используется глобальная переменная ПараметрыПроверкиОрфографииТекст в модуле управляемого приложения
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Проверяет орфографию переданного текста, возвращает Истина если текст изменен
//
// Параметры:
//  ОбработчикРезультата		 - 	 - 
//  СоответствиеТекстов - Соответствие - содержит набор текстов
//		* Ключ - наименование поля
//		* Значение - текст поля
//  ВыводитьРезультатПроверки	 - Булево - Истина, если необходимо выводить предупреждения о результатах проверки 
//
Процедура ПроверитьОрфографиюТекст(ОбработчикРезультата, СоответствиеТекстов, ВыводитьРезультатПроверки = Истина) Экспорт
	
	#Если Не ВебКлиент И Не МобильныйКлиент И Не МобильноеПриложениеКлиент Тогда
	
	СловЗаменено = 0;
	СловПропущено = 0;
	
	Попытка
		// BSLLS:UsingObjectNotAvailableUnix-off
		Word = Новый COMОбъект("Word.Application");
		// BSLLS:UsingObjectNotAvailableUnix-on

		Если Word.Documents.Count = 0 Тогда
			Word.Documents.Add();
		КонецЕсли;
		
	Исключение
		
		Word = Неопределено;
		
		СтруктураВозврата = Новый Структура("ТекстПисьма, ТекстИзменен, Реквизит", "", Ложь, "");
		ВыполнитьОбработкуОповещения(ОбработчикРезультата, СтруктураВозврата);
		
		Возврат;
		
	КонецПопытки;
	
	
	Для каждого ТекТекстПроверки Из СоответствиеТекстов Цикл
		
		ТекстДляПроверки = ТекТекстПроверки.Значение;
		
		Текст = ТекстДляПроверки;
		
		Исключения = Новый Соответствие;
		СловаЗамены = Новый Соответствие;
		
		ВсеПрочиеСловаНеПроверяем = Ложь;
		НовыйТекстовыйДокумент = Новый ТекстовыйДокумент();
		
		НомерСтрокиНачальный = 1;
		ПозицияВСтроке = -1;
		СловоЗамены = "";
		СловоОригинальное = "";
		НомерСлова = -1;
		НомерСтрокиТекущий = -1;
		ТекстПисьмаРезультирующий = "";
		
		ПараметрыПроверкиОрфографииТекст = Новый Структура;
		
		ПараметрыПроверкиОрфографииТекст.Вставить("Реквизит", ТекТекстПроверки.Ключ);
		ПараметрыПроверкиОрфографииТекст.Вставить("Текст", Текст);
		ПараметрыПроверкиОрфографииТекст.Вставить("ТекстДляПроверки", ТекстДляПроверки);
		ПараметрыПроверкиОрфографииТекст.Вставить("ТекстИзменен", Ложь);
		СоответствиеТекстовОстаток = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(СоответствиеТекстов);
		СоответствиеТекстовОстаток.Удалить(ТекТекстПроверки.Ключ);
		ПараметрыПроверкиОрфографииТекст.Вставить("СоответствиеТекстовОстаток",СоответствиеТекстовОстаток);
		ПараметрыПроверкиОрфографииТекст.Вставить("ВыводитьРезультатПроверки", ВыводитьРезультатПроверки);
		
		ПараметрыПроверкиОрфографииТекст.Вставить("НомерСтрокиНачальный", НомерСтрокиНачальный);
		ПараметрыПроверкиОрфографииТекст.Вставить("ПозицияВСтроке", ПозицияВСтроке); 
		ПараметрыПроверкиОрфографииТекст.Вставить("СловоОригинальное", СловоОригинальное);
		ПараметрыПроверкиОрфографииТекст.Вставить("СловоЗамены", СловоЗамены);
		ПараметрыПроверкиОрфографииТекст.Вставить("НомерСлова", НомерСлова);
		ПараметрыПроверкиОрфографииТекст.Вставить("Word", Word);
		ПараметрыПроверкиОрфографииТекст.Вставить("Исключения", Исключения); 
		ПараметрыПроверкиОрфографииТекст.Вставить("СловаЗамены", СловаЗамены); 
		ПараметрыПроверкиОрфографииТекст.Вставить("НомерСтрокиТекущий", НомерСтрокиТекущий);
		ПараметрыПроверкиОрфографииТекст.Вставить("СловЗаменено", СловЗаменено);
		ПараметрыПроверкиОрфографииТекст.Вставить("СловПропущено", СловПропущено);
		ПараметрыПроверкиОрфографииТекст.Вставить("Слово", "");
		
		ПараметрыПроверкиОрфографииТекст.Вставить("НоваяСтрока", "");
		ПараметрыПроверкиОрфографииТекст.Вставить("ВсеПрочиеСловаНеПроверяем", ВсеПрочиеСловаНеПроверяем);
		ПараметрыПроверкиОрфографииТекст.Вставить("НовыйТекстовыйДокумент", НовыйТекстовыйДокумент);
		ПараметрыПроверкиОрфографииТекст.Вставить("ДобавочныйСимвол", "");
		
		ПараметрыПроверкиОрфографииТекст.Вставить("ОбработчикРезультата", ОбработчикРезультата);
		ПараметрыПроверкиОрфографииТекст.Вставить("ТекстПисьмаРезультирующий", ТекстПисьмаРезультирующий);
		
		// Используем хранилище глобальных переменных.
		ИмяПараметра = "ПроверкаОрфографииКлиент.ПараметрыПроверкиОрфографии";
		ПараметрыПриложения.Вставить(ИмяПараметра, ПараметрыПроверкиОрфографииТекст);
		
		ПроверитьОрфографиюВБлокеТекста();
		
		Прервать;		
		
	КонецЦикла;
	
	#КонецЕсли
	
КонецПроцедуры

Процедура ПроверитьОрфографиюВБлокеТекста() Экспорт
	
	#Если Не ВебКлиент И Не МобильныйКлиент И Не МобильноеПриложениеКлиент Тогда
		
	ПараметрыПроверкиОрфографииТекст = ПараметрыПриложения["ПроверкаОрфографииКлиент.ПараметрыПроверкиОрфографии"]; // параметр из хранилища глобальных переменных
		
	СимволыРазделители = ПолучитьСимволыРазделители();
	
	Если Не ПараметрыПроверкиОрфографииТекст.Свойство("ТекстовыйДокумент") Тогда 
		ПараметрыПроверкиОрфографииТекст.Вставить("ТекстовыйДокумент", Новый ТекстовыйДокумент());	
		ПараметрыПроверкиОрфографииТекст.ТекстовыйДокумент.УстановитьТекст(ПараметрыПроверкиОрфографииТекст.Текст);
	КонецЕсли;		
	
	Если Не ПараметрыПроверкиОрфографииТекст.Свойство("КоличествоСтрокТекста") Тогда 
		ПараметрыПроверкиОрфографииТекст.Вставить(
			"КоличествоСтрокТекста", 
			ПараметрыПроверкиОрфографииТекст.ТекстовыйДокумент.КоличествоСтрок());
	КонецЕсли;	
	
	Если Не ПараметрыПроверкиОрфографииТекст.Свойство("НомерСтроки") Тогда 
		ПараметрыПроверкиОрфографииТекст.Вставить("НомерСтроки", ПараметрыПроверкиОрфографииТекст.НомерСтрокиНачальный);
	КонецЕсли;	
	
	Если Не ПараметрыПроверкиОрфографииТекст.Свойство("НомерПервогоСимвола") Тогда 
		ПараметрыПроверкиОрфографииТекст.Вставить("НомерПервогоСимвола", 1);
	КонецЕсли;	
	
	Если Не ПараметрыПроверкиОрфографииТекст.Свойство("НомерАнализируемогоСимвола") Тогда 
		ПараметрыПроверкиОрфографииТекст.Вставить("НомерАнализируемогоСимвола", 1);
	КонецЕсли;	
	
	ПараметрыПроверкиОрфографииТекст.НоваяСтрока = ПараметрыПроверкиОрфографииТекст.НоваяСтрока 
		+ ПараметрыПроверкиОрфографииТекст.Слово;
		
	Если СтрДлина(ПараметрыПроверкиОрфографииТекст.ДобавочныйСимвол) <> 0 Тогда
		ПараметрыПроверкиОрфографииТекст.НоваяСтрока = ПараметрыПроверкиОрфографииТекст.НоваяСтрока 
			+ ПараметрыПроверкиОрфографииТекст.ДобавочныйСимвол;
		ПараметрыПроверкиОрфографииТекст.ДобавочныйСимвол = "";
	КонецЕсли;
		
	ПараметрыПроверкиОрфографииТекст.Слово = "";	
	
	// Перебор строк
	Для СчСтроки = ПараметрыПроверкиОрфографииТекст.НомерСтроки 
		По ПараметрыПроверкиОрфографииТекст.КоличествоСтрокТекста Цикл
		
		ПараметрыПроверкиОрфографииТекст.НомерСтроки = СчСтроки;
		Если ПараметрыПроверкиОрфографииТекст.ВыводитьРезультатПроверки Тогда  // { *Олег Каратаев [2018-02-16]  }
			
			СтрокаСостояние = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Всего в тексте %1 строк. Проверяется %2 строка.'"),
				Строка(ПараметрыПроверкиОрфографииТекст.КоличествоСтрокТекста), 
				Строка(ПараметрыПроверкиОрфографииТекст.НомерСтроки));
			Состояние(СтрокаСостояние);
			
		КонецЕсли;
		
		// Получение строки, которую далее будем проверять
		СтрокаИзТекста = ПараметрыПроверкиОрфографииТекст.ТекстовыйДокумент.ПолучитьСтроку(ПараметрыПроверкиОрфографииТекст.НомерСтроки);
		
		Если ПараметрыПроверкиОрфографииТекст.ВсеПрочиеСловаНеПроверяем Тогда
			ПараметрыПроверкиОрфографииТекст.НовыйТекстовыйДокумент.ДобавитьСтроку(СтрокаИзТекста);
			Продолжить;
		КонецЕсли;	
		
		Если ПараметрыПроверкиОрфографииТекст.ПозицияВСтроке <> -1 
			И (ПараметрыПроверкиОрфографииТекст.НомерСтроки = ПараметрыПроверкиОрфографииТекст.НомерСтрокиНачальный) Тогда
			ПараметрыПроверкиОрфографииТекст.НомерПервогоСимвола = ПараметрыПроверкиОрфографииТекст.ПозицияВСтроке;
		Иначе	
			// { *Олег Каратаев [29.10.2016] В случае наличия в тексте кавычек возникает дублирование строки. Причина зачем данный код добавлен неизвестна
			ПараметрыПроверкиОрфографииТекст.НомерПервогоСимвола = 1;
			//---1С---
			//ПараметрыПроверкиОрфографииТекст.НомерПервогоСимвола = Найти(СтрокаИзТекста, """");
			//Если ПараметрыПроверкиОрфографииТекст.НомерПервогоСимвола <> 0 Тогда
			//	ПараметрыПроверкиОрфографииТекст.НомерПервогоСимвола = ПараметрыПроверкиОрфографииТекст.НомерПервогоСимвола + 1;
			//Иначе
			//	ПараметрыПроверкиОрфографииТекст.НомерПервогоСимвола = 1;
			//КонецЕсли;
			// }
		КонецЕсли;
		
		ПараметрыПроверкиОрфографииТекст.НомерСтрокиТекущий = ПараметрыПроверкиОрфографииТекст.НомерСтроки;
		
		ДлинаСтроки = СтрДлина(СтрокаИзТекста);
		ПараметрыПроверкиОрфографииТекст.НоваяСтрока = ПараметрыПроверкиОрфографииТекст.НоваяСтрока
			+ Лев(СтрокаИзТекста, ПараметрыПроверкиОрфографииТекст.НомерПервогоСимвола - 1);
		ПараметрыПроверкиОрфографииТекст.Слово       = "";
		СловоСобрано = Ложь;
		
		// Посимвольный обход строки для выделения и проверки слов
		Для СчСимволов = ПараметрыПроверкиОрфографииТекст.НомерАнализируемогоСимвола 
			По ДлинаСтроки Цикл
			
			ПараметрыПроверкиОрфографииТекст.НомерАнализируемогоСимвола = СчСимволов;
			
			ПозицияВСтроке = ПараметрыПроверкиОрфографииТекст.НомерАнализируемогоСимвола;
			Символ = Сред(СтрокаИзТекста, ПараметрыПроверкиОрфографииТекст.НомерАнализируемогоСимвола, 1);
			
			Если Найти(СимволыРазделители, Символ) <> 0 Тогда
				
				Если СтрДлина(ПараметрыПроверкиОрфографииТекст.Слово) = 0 Тогда
					ПараметрыПроверкиОрфографииТекст.НоваяСтрока = ПараметрыПроверкиОрфографииТекст.НоваяСтрока + Символ;
				Иначе
					ПараметрыПроверкиОрфографииТекст.ДобавочныйСимвол = Символ;
					СловоСобрано = Истина;
				КонецЕсли;
				
			Иначе
				
				ПараметрыПроверкиОрфографииТекст.Слово = ПараметрыПроверкиОрфографииТекст.Слово + Символ;
				Если ДлинаСтроки = ПараметрыПроверкиОрфографииТекст.НомерАнализируемогоСимвола Тогда 
					СловоСобрано = Истина;
				КонецЕсли;
				
			КонецЕсли;
			
			Если Не ПараметрыПроверкиОрфографииТекст.Свойство("Слово") Тогда 
				ПараметрыПроверкиОрфографииТекст.Вставить("Слово", "");
			КонецЕсли;	
			
			Если СловоСобрано Тогда
				
				Результат = ОрфографияОбработатьСлово(ПараметрыПроверкиОрфографииТекст.Слово, СтрокаИзТекста);
				СловоСобрано = Ложь;
				ПараметрыПроверкиОрфографииТекст.НомерАнализируемогоСимвола = 
					ПараметрыПроверкиОрфографииТекст.НомерАнализируемогоСимвола + 1;
				
				Если Не Результат Тогда
					// Процедура проверки прервана модальным окном и будет продолжена при его закрытии
					Возврат;
				КонецЕсли;	
				
				ПараметрыПроверкиОрфографииТекст.НоваяСтрока = ПараметрыПроверкиОрфографииТекст.НоваяСтрока 
					+ ПараметрыПроверкиОрфографииТекст.Слово;
					
				Если СтрДлина(ПараметрыПроверкиОрфографииТекст.ДобавочныйСимвол) <> 0 Тогда
					ПараметрыПроверкиОрфографииТекст.НоваяСтрока = ПараметрыПроверкиОрфографииТекст.НоваяСтрока 
						+ ПараметрыПроверкиОрфографииТекст.ДобавочныйСимвол;
					ПараметрыПроверкиОрфографииТекст.ДобавочныйСимвол = "";
				КонецЕсли;
					
				ПараметрыПроверкиОрфографииТекст.Слово = "";
				
			КонецЕсли;
			
		КонецЦикла;
		
		ПараметрыПроверкиОрфографииТекст.НоваяСтрока = ПараметрыПроверкиОрфографииТекст.НоваяСтрока 
			+ ПараметрыПроверкиОрфографииТекст.Слово;
		
		ПараметрыПроверкиОрфографииТекст.НомерАнализируемогоСимвола = 1;
		ПараметрыПроверкиОрфографииТекст.НовыйТекстовыйДокумент.ДобавитьСтроку(ПараметрыПроверкиОрфографииТекст.НоваяСтрока);
		ПараметрыПроверкиОрфографииТекст.НоваяСтрока = "";
		
	КонецЦикла;
	
	ЗавершитьПроверкуОрфографии();
	
	#КонецЕсли
	
КонецПроцедуры

// Завершение обработки блока текста
Процедура ЗавершениеПроверкиОрфографииВБлокеТекста(КодВозврата, Параметр) Экспорт
	
	ПараметрыПроверкиОрфографииТекст = ПараметрыПриложения["ПроверкаОрфографииКлиент.ПараметрыПроверкиОрфографии"]; // параметр из хранилища глобальных переменных
	
	Если ТипЗнч(КодВозврата) <> Тип("Структура") Тогда
		ПараметрыПроверкиОрфографииТекст.ВсеПрочиеСловаНеПроверяем = Истина;
		Кнопка = "Завершить";
	Иначе
		Кнопка = КодВозврата.Кнопка;
	КонецЕсли;	
	
	СловоИсходное = ПараметрыПроверкиОрфографииТекст.Слово;
	
	Если Кнопка = "Завершить" Тогда
		ПараметрыПроверкиОрфографииТекст.ВсеПрочиеСловаНеПроверяем = Истина;
	КонецЕсли;
	
	Если Кнопка = "ПропуститьВсе" Тогда
		ПараметрыПроверкиОрфографииТекст.Исключения.Вставить(ПараметрыПроверкиОрфографииТекст.Слово, 1);
		ПараметрыПроверкиОрфографииТекст.СловПропущено = ПараметрыПроверкиОрфографииТекст.СловПропущено + 1;
	КонецЕсли;
	
	Если Кнопка = "Пропустить" Тогда
		ПараметрыПроверкиОрфографииТекст.СловПропущено = ПараметрыПроверкиОрфографииТекст.СловПропущено + 1;
	КонецЕсли;
	
	Если Кнопка = "Заменить" Тогда
		
		ПараметрыПроверкиОрфографииТекст.СловоОригинальное = ПараметрыПроверкиОрфографииТекст.Слово;
		ПараметрыПроверкиОрфографииТекст.СловоЗамены = КодВозврата.СловоЗамены;
		ПараметрыПроверкиОрфографииТекст.Слово = КодВозврата.СловоЗамены;
		ПараметрыПроверкиОрфографииТекст.СловЗаменено = ПараметрыПроверкиОрфографииТекст.СловЗаменено + 1;
		
	КонецЕсли;
	
	Если Кнопка = "ЗаменитьВсе" Тогда
		
		ПараметрыПроверкиОрфографииТекст.СловоЗамены = КодВозврата.СловоЗамены;
		ПараметрыПроверкиОрфографииТекст.СловаЗамены.Вставить(ПараметрыПроверкиОрфографииТекст.Слово, ПараметрыПроверкиОрфографииТекст.СловоЗамены);
		ПараметрыПроверкиОрфографииТекст.СловоОригинальное = ПараметрыПроверкиОрфографииТекст.Слово;
		ПараметрыПроверкиОрфографииТекст.Слово = КодВозврата.СловоЗамены;
		
		ПараметрыПроверкиОрфографииТекст.СловЗаменено = ПараметрыПроверкиОрфографииТекст.СловЗаменено + 1;
		
	КонецЕсли;
	
	Если Кнопка = "ДобавитьИсключение" Тогда
		
		ПараметрыПроверкиОрфографииТекст.Исключения.Вставить(ПараметрыПроверкиОрфографииТекст.Слово, 1);
		ДобавитьСловоВИСключенияWord(
			ПараметрыПроверкиОрфографииТекст.Word, 
			ПараметрыПроверкиОрфографииТекст.Слово);
			
	КонецЕсли;
	
	ПроверитьОрфографиюВБлокеТекста(); 
	
КонецПроцедуры

// Возвращает символы -разделители
Функция ПолучитьСимволыРазделители() Экспорт 
	
	СимволыРазделители = " .,;:?!-+*/\%=<>[](){}""'|~@#$^&_";
	СимволыРазделители = СимволыРазделители + Символ(9) + Символ(10) + Символ(13) + Символ(160);
	Возврат СимволыРазделители;
	
КонецФункции	

// Добавляет слово в файл исключений Word
Процедура ДобавитьСловоВИсключенияWord(Word, Слово) Экспорт
	
	#Если Не ВебКлиент И Не МобильныйКлиент И Не МобильноеПриложениеКлиент Тогда
	
	Попытка
	
		ПользовательскийСловарь = Word.Application.CustomDictionaries.ActiveCustomDictionary;
	    ПолныйПутьФайла = ПользовательскийСловарь.Path + "\" + ПользовательскийСловарь.Name;
		
		Файл = Новый Файл(ПолныйПутьФайла);
		Если Не Файл.Существует() Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не найден словарь Word: %1'"), ПолныйПутьФайла);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;	
		
		Кодировка = ОпределитьКодировкуТекстовогоФайла(ПолныйПутьФайла);
	
		// Дописывать = Истина
		ЗаписьТекста = Новый ЗаписьТекста(ПолныйПутьФайла, Кодировка, , Истина);
		ЗаписьТекста.ЗаписатьСтроку(Слово);
		ЗаписьТекста.Закрыть();
		
	Исключение
		
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначенияКлиент.СообщитьПользователю(ОписаниеОшибки);
		
	КонецПопытки;
	
	#КонецЕсли
	
КонецПроцедуры	

// Вычисляет кодировку 
Функция ОпределитьКодировкуТекстовогоФайла(ПолноеИмяФайла) Экспорт
	
	Кодировка = Неопределено;
	#Если Не ВебКлиент И Не МобильныйКлиент И Не МобильноеПриложениеКлиент Тогда
	
	// Определяем кодировку
		
	ЧтениеТекста = Новый ЧтениеТекста(ПолноеИмяФайла, КодировкаТекста.Системная);
	
	// Проверяем наличие BOM (Byte order mark)
	Символ0 = ЧтениеТекста.Прочитать(1);
	Символ1 = ЧтениеТекста.Прочитать(1);
	Символ2 = ЧтениеТекста.Прочитать(1);
	
	КодСимвола0 = КодСимвола(Символ0);
	КодСимвола1 = КодСимвола(Символ1);
	КодСимвола2 = КодСимвола(Символ2);
	
	Если КодСимвола0 = 1087 И КодСимвола1 = 187 И КодСимвола2 = 1111 Тогда
		Кодировка = "UTF-8";
	ИначеЕсли КодСимвола0 = 1103 И КодСимвола1 = 1102 Тогда
		Кодировка = "UTF-16";
	Иначе	
		Кодировка = "windows-1251";
	КонецЕсли;
		
	#КонецЕсли

	Возврат Кодировка;
	
КонецФункции	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОрфографияОбработатьСлово(Слово, СтрокаИзТекста)
	
	ПараметрыПроверкиОрфографииТекст = ПараметрыПриложения["ПроверкаОрфографииКлиент.ПараметрыПроверкиОрфографии"]; // параметр из хранилища глобальных переменных
				
	СловоНужноПроверитьWord = Истина;
	
	НаличиеСлова = ПараметрыПроверкиОрфографииТекст.Исключения.Получить(Слово);
	Если НаличиеСлова <> Неопределено Тогда
		СловоНужноПроверитьWord = Ложь;
		ПараметрыПроверкиОрфографииТекст.СловПропущено = ПараметрыПроверкиОрфографииТекст.СловПропущено + 1;
	КонецЕсли;	
	
	ЗамененноеСлово = ПараметрыПроверкиОрфографииТекст.СловаЗамены.Получить(Слово);
	Если ЗамененноеСлово <> Неопределено Тогда
		Слово = ЗамененноеСлово;
		ПараметрыПроверкиОрфографииТекст.СловЗаменено = ПараметрыПроверкиОрфографииТекст.СловЗаменено + 1;
	КонецЕсли;	
	
	Если СловоНужноПроверитьWord Тогда
		
		Если Не ПараметрыПроверкиОрфографииТекст.Word.CheckSpelling(Слово) Тогда
			
			СписокВариантов = Новый СписокЗначений;
			
			ВариантыЗамены = ПараметрыПроверкиОрфографииТекст.Word.getSpellingSuggestions(Слово);
			
			Если ВариантыЗамены.Count <> 0 Тогда
				Для НомерВарианта = 1 По ВариантыЗамены.Count Цикл
					СписокВариантов.Добавить(ВариантыЗамены.Item(НомерВарианта).Name);
				КонецЦикла;
			КонецЕсли;
			
			ПараметрыФормы = Новый Структура("ИсходнаяСтрока,СловоЗамены,СписокВариантов,ПараметрыПроверкиОрфографииТекст");
			ПараметрыФормы.ИсходнаяСтрока = СокрЛП(СтрокаИзТекста);
			ПараметрыФормы.СловоЗамены = Слово;
			ПараметрыФормы.СписокВариантов = СписокВариантов.Скопировать();
			
			ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеПроверкиОрфографииВБлокеТекста", ЭтотОбъект);
			ОткрытьФорму(
				"ОбщаяФорма.ПроверкаОрфографии", 
				ПараметрыФормы,
				,,,,
				ОписаниеОповещения,
				РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
				
			Возврат Ложь;	
			
		КонецЕсли;
		
	КонецЕсли;
	
	СловоСобрано = Ложь;
	
	Возврат Истина;
	
КонецФункции

Процедура ЗавершитьПроверкуОрфографии()
	
	ПараметрыПроверкиОрфографииТекст = ПараметрыПриложения["ПроверкаОрфографииКлиент.ПараметрыПроверкиОрфографии"]; // параметр из хранилища глобальных переменных
	
	Текст = ПараметрыПроверкиОрфографииТекст.НовыйТекстовыйДокумент.ПолучитьТекст();
	Если Прав(Текст, 1) = Символы.ПС Тогда
		Текст = Лев(Текст, СтрДлина(Текст)-1);
	КонецЕсли;
	
	ТекстПисьма = ПараметрыПроверкиОрфографииТекст.ТекстДляПроверки;
	ТекстИзменен = Ложь;
	
	Если СокрЛП(ПараметрыПроверкиОрфографииТекст.ТекстДляПроверки) <> СокрЛП(Текст) Тогда
		
		// Для текстового письма целиком собираем новый текст (с исправленными словами) и ставим его письму.
		ТекстПисьма = Текст;
		ТекстИзменен = Истина;
		ПараметрыПроверкиОрфографииТекст.ТекстИзменен = ТекстИзменен;
		
	КонецЕсли;
	
	ПараметрыПроверкиОрфографииТекст.Word.Quit();
	ПараметрыПроверкиОрфографииТекст.Word = Неопределено;
	
	Если ПараметрыПроверкиОрфографииТекст.ВыводитьРезультатПроверки Тогда  // { *Олег Каратаев [2018-02-16]  }
	
		Состояние();
		
		ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Проверка орфографии завершена. 
			|Заменено слов: %1, пропущено слов: %2.'"),
			ПараметрыПроверкиОрфографииТекст.СловЗаменено, ПараметрыПроверкиОрфографииТекст.СловПропущено);
		
			ПоказатьПредупреждение(, ТекстПредупреждения);
	
	КонецЕсли;
	
	// Выход из рекурсии или продолжение работы.
	Если ПараметрыПроверкиОрфографииТекст.СоответствиеТекстовОстаток.Количество()=0 Или ТекстИзменен Тогда
		
		СтруктураВозврата = Новый Структура("ТекстПисьма, ТекстИзменен, Реквизит", ТекстПисьма, ТекстИзменен, ПараметрыПроверкиОрфографииТекст.Реквизит);
		ВыполнитьОбработкуОповещения(ПараметрыПроверкиОрфографииТекст.ОбработчикРезультата, СтруктураВозврата);
	
		ПараметрыПроверкиОрфографииТекст = Неопределено;
		
	Иначе
		
		ПроверитьОрфографиюТекст(ПараметрыПроверкиОрфографииТекст.ОбработчикРезультата, ПараметрыПроверкиОрфографииТекст.СоответствиеТекстовОстаток, ПараметрыПроверкиОрфографииТекст.ВыводитьРезультатПроверки);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
