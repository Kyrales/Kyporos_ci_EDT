
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Начать фиксацию времени на задачу
//
// Параметры:
//  Исполнитель	 - СправочникСсылка.Пользователи 
//  Задача		 - ЗадачаСсылка.ЗадачиПользователя 
//  ТипРаботы	 - ПеречислениеСсылка.ТипыРаботПоЗадачам
//
Процедура НачатьРаботуСЗадачей(Исполнитель, Задача, Знач ТипРаботы = Неопределено) Экспорт
	
	Если ТипРаботы = Неопределено Тогда
		
		ТипРаботы = Перечисления.ТипыРаботПоЗадачам.ПустаяСсылка();
		
	КонецЕсли;
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Исполнитель = Исполнитель;
	МенеджерЗаписи.Прочитать();
	
	Если МенеджерЗаписи.Выбран()
		И МенеджерЗаписи.Задача = Задача
		И МенеджерЗаписи.ТипРаботы = ТипРаботы Тогда
		// В работе находится текущая задача, ничего не меняем.
		
		Возврат;
		
	КонецЕсли;
	
	ТекущаяДата = ТекущаяДатаСеанса();
	
	НачатьТранзакцию();
	Попытка
		
		Если МенеджерЗаписи.Выбран() Тогда
			
			// В работе находится другая задача. Учтем время, затраченное на нее.
			РегистрыСведений.УчетРабочегоВремени.ЗафиксироватьПотраченноеВремяПоЗадаче(Исполнитель, МенеджерЗаписи.Задача, 
				МенеджерЗаписи.ДатаНачала, ТекущаяДата, МенеджерЗаписи.ТипРаботы);
			
		Иначе
			
			МенеджерЗаписи.Исполнитель = Исполнитель;
			
		КонецЕсли;
		
		МенеджерЗаписи.Задача = Задача;
		МенеджерЗаписи.ДатаНачала = ТекущаяДата;
		МенеджерЗаписи.ТипРаботы = ТипРаботы;
		МенеджерЗаписи.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение; 
		
	КонецПопытки;
	
КонецПроцедуры

// Приостановить работу с задачей
//
// Параметры:
//  Исполнитель	 - СправочникСсылка.Пользователи 
//  Задача		 - ЗадачаСсылка.ЗадачиПользователя 
//  Комментарий	 - Строка
//
Процедура ПриостановитьРаботуСЗадачей(Исполнитель, Задача, Комментарий = "") Экспорт
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Исполнитель = Исполнитель;
	МенеджерЗаписи.Прочитать();
	
	Если Не МенеджерЗаписи.Выбран() Тогда
		Возврат;
	КонецЕсли;
	
	Если МенеджерЗаписи.Задача <> Задача Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяДата = ТекущаяДатаСеанса();
	
	НачатьТранзакцию();
	
	Попытка
				
		РегистрыСведений.УчетРабочегоВремени.ЗафиксироватьПотраченноеВремяПоЗадаче(Исполнитель, Задача, МенеджерЗаписи.ДатаНачала, 
			ТекущаяДата, МенеджерЗаписи.ТипРаботы, Комментарий);
		МенеджерЗаписи.Удалить();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение; 
		
	КонецПопытки;
				
КонецПроцедуры

#КонецОбласти

#КонецЕсли
