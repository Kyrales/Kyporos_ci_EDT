#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	// Чтение
	//#ПоЗначениямИНаборамРасширенный("РегистрСведений.ЗатраченноеВремяПоЗадачам", "", "",
	//"",
	//"",
	//"Объект","Т.Задача","ИЛИ", 
	//"Пользователи","Т.Задача.ГруппаИсполнителейЗадач","", 
	//"","","", "","","", "","","", "","","", "","","", "","","", "","","", "","","", "","","", "","","", "","","", "","","", "","","", "","","")
	
	// Изменение
	//#ПоЗначениямИНаборамРасширенный("РегистрСведений.ЗатраченноеВремяПоЗадачам", "", "",
	//"",
	//"",
	//"Объект","Т.Задача","ИЛИ", 
	//"Пользователи","Т.Задача.ГруппаИсполнителейЗадач","", 
	//"","","", "","","", "","","", "","","", "","","", "","","", "","","", "","","", "","","", "","","", "","","", "","","", "","","", "","","")
	
    Ограничение.Текст =
    "РазрешитьЧтение
    |ГДЕ
    |    ЧтениеОбъектаРазрешено(Задача)
    |;
    |РазрешитьИзменениеЕслиРазрешеноЧтение
    |ГДЕ
    |    ИзменениеОбъектаРазрешено(Задача)";
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// Функция возвращает сумму затраченного времени по указанным параметрам
//
// Параметры:
//  Задача  - ЗадачаСсылка.ЗадачиПользователя - основной параметр
//
// Возвращаемое значение:
//	Структура - содержит свойства:
//		* ОбщееЗатраченноеВремя - Число  - затраченное время (в случае если не определено, то возвращает 0)
//		* ВремяИсполнителей - Соответствие - содержит соответствие:
//			** Ключ - СправочникСсылка.Пользователи
//			** Значение - Число - затраченное время, конкретного исполнителя
//		* ДеталиПоЗатраченномуВремени - ТаблицаЗначений - содержит колонки:
//			** Период - Дата - дата выполнения работы
//			** Исполнитель - СправочникСсылка.Пользователи
//			** ВидРаботы - СправочникСсылка.ВидыРабот
//			** ЗатраченноеВремя - Число
//			** Описание - Строка
//
Функция ФактическоеЗатраченноеВремяПоЗадаче(Задача) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("ОбщееЗатраченноеВремя", 0);
	СтруктураРезультата.Вставить("ВремяИсполнителей", Новый Соответствие);
	ДеталиПоЗатраченномуВремени = Новый ТаблицаЗначений;
	ДеталиПоЗатраченномуВремени.Колонки.Добавить("Период");
	ДеталиПоЗатраченномуВремени.Колонки.Добавить("Исполнитель");
	ДеталиПоЗатраченномуВремени.Колонки.Добавить("ВидРаботы");
	ДеталиПоЗатраченномуВремени.Колонки.Добавить("ЗатраченноеВремя");
	ДеталиПоЗатраченномуВремени.Колонки.Добавить("Описание");
	СтруктураРезультата.Вставить("ДеталиПоЗатраченномуВремени", ДеталиПоЗатраченномуВремени);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗатраченноеВремяЗадачи.Задача КАК Задача,
	               |	ЗатраченноеВремяЗадачи.Исполнитель КАК Исполнитель,
	               |	СУММА(ЗатраченноеВремяЗадачи.ЗатраченноеВремя) КАК ЗатраченноеВремя
	               |ИЗ
	               |	РегистрСведений.ЗатраченноеВремяПоЗадачам КАК ЗатраченноеВремяЗадачи
	               |ГДЕ
	               |	ЗатраченноеВремяЗадачи.Задача = &Задача
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ЗатраченноеВремяЗадачи.Задача,
	               |	ЗатраченноеВремяЗадачи.Исполнитель
	               |ИТОГИ
	               |	СУММА(ЗатраченноеВремя)
	               |ПО
	               |	Задача
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЗатраченноеВремяЗадачи.Период КАК Период,
	               |	ЗатраченноеВремяЗадачи.Исполнитель КАК Исполнитель,
	               |	ЗатраченноеВремяЗадачи.ВидРаботы КАК ВидРаботы,
	               |	ЗатраченноеВремяЗадачи.ЗатраченноеВремя КАК ЗатраченноеВремя,
	               |	ЗатраченноеВремяЗадачи.Описание КАК Описание
	               |ИЗ
	               |	РегистрСведений.ЗатраченноеВремяПоЗадачам КАК ЗатраченноеВремяЗадачи
	               |ГДЕ
	               |	ЗатраченноеВремяЗадачи.Задача = &Задача
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Период";
	
	Запрос.УстановитьПараметр("Задача", Задача);
	
	Результат = Запрос.ВыполнитьПакет();
	ВыборкаИтоги = Результат[0].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Задача");
	
	Если ВыборкаИтоги.Следующий() Тогда
	
		СтруктураРезультата.ОбщееЗатраченноеВремя = ВыборкаИтоги.ЗатраченноеВремя;	
	
		Выборка = ВыборкаИтоги.Выбрать();
		
		Пока Выборка.Следующий() Цикл
		    // по исполнителям
			СтруктураРезультата.ВремяИсполнителей.Вставить(Выборка.Исполнитель, Выборка.ЗатраченноеВремя);
		
		КонецЦикла;
		
	КонецЕсли;
	
	ВыборкаДеталей = Результат[1].Выбрать();
	Пока ВыборкаДеталей.Следующий() Цикл
	
		НовСтрока = СтруктураРезультата.ДеталиПоЗатраченномуВремени.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтрока, ВыборкаДеталей);
	
	КонецЦикла;
	
	Возврат СтруктураРезультата;

КонецФункции // ФактическоеЗатраченноеВремяПоЗадаче()

#КонецОбласти

#КонецЕсли
