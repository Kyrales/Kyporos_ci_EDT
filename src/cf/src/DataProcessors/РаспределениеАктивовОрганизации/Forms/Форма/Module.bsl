
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	
	ЗаполнитьСписок();  
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписок()

	  Запрос = Новый Запрос;
	  Запрос.Текст = "ВЫБРАТЬ
	                 |	РабочиеМеста.Ссылка КАК РабочееМесто,
	                 |	ЕСТЬNULL(ВложенныйЗапрос.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК Номенклатура,
	                 |	ЕСТЬNULL(ВложенныйЗапрос.Количество, 0) КАК Используется
	                 |ИЗ
	                 |	Справочник.РабочиеМеста КАК РабочиеМеста
	                 |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	                 |			УчетОборудованияРабочихМест.Организация КАК Организация,
	                 |			УчетОборудованияРабочихМест.РабочееМесто КАК РабочееМесто,
	                 |			УчетОборудованияРабочихМест.Номенклатура КАК Номенклатура,
	                 |			УчетОборудованияРабочихМест.Количество КАК Количество
	                 |		ИЗ
	                 |			РегистрСведений.УчетОборудованияРабочихМест КАК УчетОборудованияРабочихМест
	                 |		ГДЕ
	                 |			УчетОборудованияРабочихМест.Номенклатура.ВидНоменклатуры = &ВидНоменклатуры
	                 |			И УчетОборудованияРабочихМест.Организация = &Организация) КАК ВложенныйЗапрос
	                 |		ПО РабочиеМеста.Ссылка = ВложенныйЗапрос.РабочееМесто
	                 |ГДЕ
	                 |	РабочиеМеста.Организация = &Организация
	                 |	И РабочиеМеста.СписокРабочихИнструментов.ВидНоменклатуры = &ВидНоменклатуры
	                 |
	                 |УПОРЯДОЧИТЬ ПО
	                 |	РабочиеМеста.Наименование";
	  
	  Запрос.УстановитьПараметр("Организация", Объект.Организация);
	  Запрос.УстановитьПараметр("ВидНоменклатуры", Объект.ВидНоменклатуры);
	  
	  Результат = Запрос.Выполнить();
	  Выборка = Результат.Выгрузить();
	  
	  Объект.СписокРабочихМест.Загрузить(Выборка);
	  

      //Для поля ИнформацияПоАктивам
	  
	  Запрос = Новый Запрос;
	  Запрос.Текст = "ВЫБРАТЬ
	                 |	ВложенныйЗапрос.Номенклатура,
	                 |	ВложенныйЗапрос.Номенклатура.КоличествоХарактеристика КАК КоличествоХарактеристика,
	                 |	ВложенныйЗапрос.Количество
	                 |ИЗ
	                 |	(ВЫБРАТЬ
	                 |		УчетОборудованияРабочихМест.Номенклатура КАК Номенклатура,
	                 |		СУММА(УчетОборудованияРабочихМест.Количество) КАК Количество
	                 |	ИЗ
	                 |		РегистрСведений.УчетОборудованияРабочихМест КАК УчетОборудованияРабочихМест
	                 |	ГДЕ
	                 |		УчетОборудованияРабочихМест.Номенклатура.ВидНоменклатуры = &ВидНоменклатуры
	                 |	
	                 |	СГРУППИРОВАТЬ ПО
	                 |		УчетОборудованияРабочихМест.Номенклатура) КАК ВложенныйЗапрос
	                 |
	                 |УПОРЯДОЧИТЬ ПО
	                 |	ВложенныйЗапрос.Номенклатура.Наименование";
	  
	  Запрос.УстановитьПараметр("Организация", Объект.Организация);
	  Запрос.УстановитьПараметр("ВидНоменклатуры", Объект.ВидНоменклатуры);
	  
	  Результат = Запрос.Выполнить();
	  Выборка = Результат.Выбрать();
	  
	  Объект.ИнформацияПоАктивам = "По "+Объект.ВидНоменклатуры.Наименование+":"+Символы.ПС;
	  
	  Пока Выборка.Следующий() Цикл
	  
		  Остаток = Выборка.КоличествоХарактеристика - Выборка.Количество;
		  ТекстИнформацииПоАктивам = СтрШаблон(НСтр("ru = '%1 - %2: %3 (%4) остаток: %5
                                                     |'"), "", Выборка.Номенклатура, Выборка.Количество, Выборка.КоличествоХарактеристика, Остаток);
		  Объект.ИнформацияПоАктивам = Объект.ИнформацияПоАктивам + ТекстИнформацииПоАктивам;
			
	  КонецЦикла;
	  
	  КолВоНеиспользуемых = Объект.СписокРабочихМест.Количество() - Объект.СписокРабочихМест.Итог("Используется");
	  ТекстИнформацииПоАктивам = СтрШаблон(НСтр("ru = '%1
                                                 |
                                                 |Осталось не задействованных рабочих мест (по выбранной организации): %2'"), Объект.ИнформацияПоАктивам, КолВоНеиспользуемых);
	  Объект.ИнформацияПоАктивам = ТекстИнформацииПоАктивам;
	  
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИзменения(Команда)
	
	СохранитьИзменениеАктивовРабочихМест();
	
	ЗаполнитьСписок();
	
КонецПроцедуры

&НаСервере
Процедура СохранитьИзменениеАктивовРабочихМест()

	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Необходимо указать организацию.'"),,"Объект.Организация");
		Возврат;
	КонецЕсли;
	
	Для каждого ТекСтрокаРабочегоМеста Из Объект.СписокРабочихМест Цикл
		
		Если ТекСтрокаРабочегоМеста.РабочееМесто.Пустая() 
			Или (ТекСтрокаРабочегоМеста.Номенклатура.Пустая() 
				И ТекСтрокаРабочегоМеста.Используется=0) Тогда
			Продолжить;
		КонецЕсли;		
		
		//Проверка остатка
		СтруктураДанных = Новый Структура;
		СтруктураДанных.Вставить("РабочееМесто",ТекСтрокаРабочегоМеста.РабочееМесто);
		СтруктураДанных.Вставить("ВидНоменклатуры",Объект.ВидНоменклатуры);
		НайдДанные = УправлениеИТАктивами.ДанныеОбАктивах(СтруктураДанных);
		Если Не НайдДанные.Организация.Пустая() Тогда
			НоменклатураВыбр = НайдДанные.Номенклатура;
		Иначе
			НоменклатураВыбр = ТекСтрокаРабочегоМеста.Номенклатура;			
		КонецЕсли;
		
		ЗаписьУчетаОборудования = РегистрыСведений.УчетОборудованияРабочихМест.СоздатьМенеджерЗаписи();
		ЗаписьУчетаОборудования.Организация  = Объект.Организация;
		ЗаписьУчетаОборудования.РабочееМесто = ТекСтрокаРабочегоМеста.РабочееМесто;
		ЗаписьУчетаОборудования.Номенклатура = НоменклатураВыбр;
		ЗаписьУчетаОборудования.Прочитать();
		
		Если НЕ ЗаписьУчетаОборудования.Выбран() Тогда
			
			ЗаписьУчетаОборудования = РегистрыСведений.УчетОборудованияРабочихМест.СоздатьМенеджерЗаписи();
			ЗаписьУчетаОборудования.Организация  = Объект.Организация;
			ЗаписьУчетаОборудования.РабочееМесто = ТекСтрокаРабочегоМеста.РабочееМесто;
			ЗаписьУчетаОборудования.Номенклатура = ТекСтрокаРабочегоМеста.Номенклатура;
			
		ИначеЕсли ЗаписьУчетаОборудования.Выбран() 
			И НоменклатураВыбр <> ТекСтрокаРабочегоМеста.Номенклатура Тогда
			
			ЗаписьУчетаОборудования.Удалить();
			ЗаписьУчетаОборудования = РегистрыСведений.УчетОборудованияРабочихМест.СоздатьМенеджерЗаписи();
			ЗаписьУчетаОборудования.Организация  = Объект.Организация;
			ЗаписьУчетаОборудования.РабочееМесто = ТекСтрокаРабочегоМеста.РабочееМесто;
			ЗаписьУчетаОборудования.Номенклатура = ТекСтрокаРабочегоМеста.Номенклатура;	
			
		КонецЕсли;
		
		ЗаписьУчетаОборудования.Количество = 1;
		Если Не (ТекСтрокаРабочегоМеста.Используется=1 
			И ТекСтрокаРабочегоМеста.Номенклатура.Пустая()) Тогда
		   	ЗаписьУчетаОборудования.Записать();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

